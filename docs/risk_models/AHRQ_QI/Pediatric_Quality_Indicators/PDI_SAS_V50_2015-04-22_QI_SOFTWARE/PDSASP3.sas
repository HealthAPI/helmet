*=========================================================================================;
*  TITLE:  PROGRAM P3: CALCULATES RISK-ADJUSTED PROVIDER
*          RATES FOR AHRQ PEDIATRIC QUALITY INDICATORS
*
*  DESCRIPTION:
*         USES MCMC TO CALCULATE RISK-ADJUSTED RATES FOR
*         PEDIATRIC QUALITY INDICATORS.
*         ADJUSTS FOR: BIRTHCAT, AGE, SEXCAT, DRG AND COMORBIDITY
*
*             >>>  VERSION 5.0.1 - APRIL 22, 2015  <<<
*
*  USER NOTE: VERSION 2.0 AHRQ QI SAS SOFTWARE DOES NOT EXPLICITLY SUPPORT THE CALCULATION 
*             OF WEIGHTED ESTIMATES AND STANDARD ERRORS USING COMPLEX SAMPLING DESIGNS.
*             THE VARIABLE DISCWT HAS BEEN REMOVED FROM ALL AHHRQ SAS QI SOFTWARE
*
*             IN ORDER TO OBTAIN WEIGHTED NATIONALLY REPRESENTATIVE ESTIMATES, ADDITIONAL 
*             CALCULATIONS WILL NEED TO BE PERFORMED.  
*             FOR A MORE THOROUGH DESCRIPTION OF WEIGHTED AHRQ QI ANALYSES BEGINNING WITH 
*             AHRQ QI SAS VERSION 4.1, SEE THE TECHNICAL DOCUMENTATION ON THE AHRQ WEBSITE.
*========================================================================================;
FILENAME CONTROL 'C:\PATHNAME\CONTROL_PDI.SAS';  *<===USER MUST MODIFY;


%INCLUDE CONTROL;

TITLE2 'PROGRAM P3 PART I';
TITLE3 'AHRQ INPATIENT QUALITY INDICATORS: CALCULATE RISK-ADJUSTED PROVIDER RATES';

*---------------------------------------------------------------------*;
*-- MAKCOVAR CREATES THE SETS OF VARIABLES USED FOR RISK ADJUSTMENT --*;
*-- THE USER MUST ENSURE THAT THE CONTROL FILE INCLUDES THE CORRECT --*;
*-- LOCATION FOR THE MAKCOVAR SAS PROGRAM.                          --*;
*---------------------------------------------------------------------*;

%INCLUDE MAKCOVAR;


 %MACRO MOD3(PD, CV);
 
 DATA   TEMP1 ;

 SET    IN1.&INFILE1.;


 *--  DATA STEP CODE TO CREATE REGRESSION COVARIATES --*;
 %MAKEVARS_PDI_&PD. ;

 RUN;                          


*-- CHOSE PARAMETER FILE USED FOR SCORING --*;

%IF &USEPOA=1 %THEN %DO ;

FILENAME RACOEFFS  "&RADIR.\QI50_PDI_&PD._POA.CSV";

%END ;

%IF &USEPOA=0 %THEN %DO ;

FILENAME RACOEFFS  "&RADIR.\QI50_PDI_&PD._NOPOA.CSV";

%END ;


*-- LOAD CSV PARAMTERS & SHAPE DATA  --*;

DATA TEMP1_MODEL ;

LENGTH VARIABLE $10 DF ESTIMATE 8 ;
  INFILE RACOEFFS DSD DLM=',' LRECL=1024;
  INPUT VARIABLE DF ESTIMATE ;
RUN ;

PROC TRANSPOSE DATA=TEMP1_MODEL OUT=TEMP2_MODEL  ;
VAR ESTIMATE ;
RUN ;

DATA MODEL_PD&PD. (KEEP=INTERCEPT XCV1-XCV&CV. _NAME_ _TYPE_) ; 
  SET TEMP2_MODEL  ;

  RENAME COL1=INTERCEPT ;
  %DO I_=1 %TO &CV. ;
  RENAME COL%EVAL(&I_ + 1) = XCV&I_ ;
  %END ;

  _NAME_ = "MHAT" ;
  _TYPE_ = "PARMS" ;

RUN ;

*-- APPLY PROC SCORE TO DATA --* ;                   
 PROC   SCORE DATA=TEMP1 SCORE=MODEL_PD&PD. TYPE=PARMS OUT=TEMP1Y;
 VAR    XCV1-XCV&CV. ;
 RUN;

 %LET DSID=%SYSFUNC(OPEN(TEMP1Y));
 %LET DNUM=%SYSFUNC(ATTRN(&DSID,NOBS));
 %LET DRC=%SYSFUNC(CLOSE(&DSID));

 %IF &DNUM NE 0 %THEN %DO;

 *-- CALCULATE PREDICTED VALUES (EHAT) --* ;
 DATA   TEMP1Y;
 SET    TEMP1Y;

 EHAT = EXP(MHAT)/(1 + EXP(MHAT));
 PHAT = EHAT * (1 - EHAT);

 RUN;



 *-- SUMMARIZE BY VARIOUS CLASSES --*;

 PROC CONTENTS DATA=TEMP1Y ;
 RUN ;

 PROC   SUMMARY DATA=TEMP1Y;
    CLASS HOSPID BWHTCAT AGEDCAT PAGECAT SEXCAT PAYCAT RACECAT;
    VAR   TPPD&PD. EHAT PHAT;
    OUTPUT OUT=RPPD&PD. SUM(TPPD&PD. EHAT PHAT)=TPPD&PD. EHAT PHAT
           SUMWGT(TPPD&PD.)=PPPD&PD.
           N=DENOM;
 RUN;


 *-- APPLY RISK ADJUSTMENT & SMOOTHING --* ;
 DATA   RPPD&PD.(KEEP=HOSPID BWHTCAT AGEDCAT PAGECAT SEXCAT PAYCAT RACECAT _TYPE_
                 EPPD&PD. RPPD&PD. LPPD&PD. UPPD&PD. SPPD&PD. XPPD&PD. VPPD&PD.
                 TPPD&PD. PPPD&PD. OPPD&PD. 
                 );
 SET    RPPD&PD.;

 IF _TYPE_ &TYPELVLP;

 *-- LOAD SIGNAL VARIANCE AND POPULATION REFERENCE RATE ARRAYS--* ;

 %INCLUDE MSXP;

 *-- MAP MEASURE NUM TO ARRAY INDEX SUB_N --* ;
 IF "&PD." = "01"  THEN SUB_N = 1;
 IF "&PD." = "02"  THEN SUB_N = 2;
 IF "&PD." = "05"  THEN SUB_N = 3;
 IF "&PD." = "06"  THEN SUB_N = 4;
 IF "&PD." = "08"  THEN SUB_N = 5;
 IF "&PD." = "09"  THEN SUB_N = 6;
 IF "&PD." = "10"  THEN SUB_N = 7;
 IF "&PD." = "11"  THEN SUB_N = 8;
 IF "&PD." = "12"  THEN SUB_N = 9;
 IF "&PD." = "70"  THEN SUB_N = 10;
 IF "&PD." = "71"  THEN SUB_N = 11;
 IF "&PD." = "72"  THEN SUB_N = 12;

 *-- T = NUMERATOR     --* ;
 *-- P = DENOMINATOR   --* ;
 *-- E = EXPECTED      --* ;
 *-- R = RISK ADJUSTED --* ; 
 *-- L = LOWER CI      --* ;
 *-- U = UPPER CI      --* ;
 *-- S = SMOOTHED      --* ;
 *-- X = SMOOTHED SE   --* ;
 *-- V = VARIANCE      --* ; 

 EPPD&PD. = EHAT / PPPD&PD.;
 THAT = TPPD&PD. / PPPD&PD.;
 OPPD&PD. = TPPD&PD. / PPPD&PD.;
 
 IF _TYPE_ IN (0,64) THEN DO;
    RPPD&PD.   = (THAT / EPPD&PD.) * ARRYP3(SUB_N);
    SE&PD.  = (ARRYP3(SUB_N) / EPPD&PD.) * (1 / PPPD&PD.) * SQRT(PHAT);
    VPPD&PD.   = SE&PD.**2;
    SN&PD.  = ARRYP2(SUB_N) / (ARRYP2(SUB_N) + VPPD&PD.);
    SPPD&PD.   = (RPPD&PD. * SN&PD.) + ((1 -  SN&PD.) * ARRYP3(SUB_N));
    XPPD&PD.   = SQRT(ARRYP2(SUB_N)- (SN&PD. * ARRYP2(SUB_N)));
 END;
 ELSE DO;
    RPPD&PD.   = (THAT / EPPD&PD.);
    SE&PD.  = (1 / EPPD&PD.) * (1 / PPPD&PD.) * SQRT(PHAT);
    VPPD&PD.   = .;
    SPPD&PD.   = .;
    XPPD&PD.   = .;
 END;
 
 LPPD&PD.   = RPPD&PD. - (1.96 * SE&PD.);
 IF LPPD&PD. < 0 THEN LPPD&PD. = 0;
 UPPD&PD.   = RPPD&PD. + (1.96 * SE&PD.);

 RUN;

 %END;

 %ELSE %DO;

 DATA   RPPD&PD.;
    HOSPID=.;BWHTCAT=.;AGEDCAT=.;PAGECAT=.;SEXCAT=.;PAYCAT=.;RACECAT=.;_TYPE_=0;
    EPPD&PD=.;RPPD&PD=.;LPPD&PD=.;UPPD&PD=.;SPPD&PD=.;XPPD&PD=.;VPPD&PD=.;
    TPPD&PD=. ; PPPD&PD=. ; 
    OUTPUT;
 RUN;

 %END;

 PROC SORT DATA=RPPD&PD.;
   BY HOSPID BWHTCAT AGEDCAT PAGECAT SEXCAT PAYCAT RACECAT _TYPE_ ;
 RUN; QUIT;
   
 PROC   DATASETS NOLIST;
 DELETE TEMP1 TEMP1Y TEMP1_MODEL TEMP2_MODEL;
 RUN;
 

%MEND MOD3;

%MOD3(01,12) ;
%MOD3(02,5) ;
%MOD3(05,3) ;
%MOD3(06,11) ;
%MOD3(08,2) ;
%MOD3(09,16) ;
%MOD3(10,12) ;
%MOD3(11,2) ;
%MOD3(12,34) ;
%MOD3(70,2) ;
%MOD3(71,30) ;
%MOD3(72,10) ;



 * --- MERGES THE AREA ADJUSTED RATES FOR EACH PD. PREFIX FOR THE - ;
 * --- ADJUSTED RATES IS (R) AND EXPECTED RATES IS (E).           - ;


 DATA RISKADJ;
 MERGE
       RPPD01(KEEP=HOSPID BWHTCAT AGEDCAT PAGECAT SEXCAT PAYCAT RACECAT 
                   EPPD01 RPPD01 LPPD01 UPPD01 PPPD01 SPPD01 XPPD01 VPPD01 TPPD01 PPPD01 OPPD01)

       RPPD02(KEEP=HOSPID BWHTCAT AGEDCAT PAGECAT SEXCAT PAYCAT RACECAT 
                   EPPD02 RPPD02 LPPD02 UPPD02 PPPD02 SPPD02 XPPD02 VPPD02 TPPD02 PPPD02 OPPD02)

       RPPD05(KEEP=HOSPID BWHTCAT AGEDCAT PAGECAT SEXCAT PAYCAT RACECAT 
                   EPPD05 RPPD05 LPPD05 UPPD05 PPPD05 SPPD05 XPPD05 VPPD05 TPPD05 PPPD05 OPPD05) 

       RPPD06(KEEP=HOSPID BWHTCAT AGEDCAT PAGECAT SEXCAT PAYCAT RACECAT 
                   EPPD06 RPPD06 LPPD06 UPPD06 PPPD06 SPPD06 XPPD06 VPPD06 TPPD06 PPPD06 OPPD06)

       RPPD08(KEEP=HOSPID BWHTCAT AGEDCAT PAGECAT SEXCAT PAYCAT RACECAT 
                   EPPD08 RPPD08 LPPD08 UPPD08 PPPD08 SPPD08 XPPD08 VPPD08 TPPD08 PPPD08 OPPD08)

       RPPD09(KEEP=HOSPID BWHTCAT AGEDCAT PAGECAT SEXCAT PAYCAT RACECAT 
                   EPPD09 RPPD09 LPPD09 UPPD09 PPPD09 SPPD09 XPPD09 VPPD09 TPPD09 PPPD09 OPPD09)

       RPPD10(KEEP=HOSPID BWHTCAT AGEDCAT PAGECAT SEXCAT PAYCAT RACECAT 
                   EPPD10 RPPD10 LPPD10 UPPD10 PPPD10 SPPD10 XPPD10 VPPD10 TPPD10 PPPD10 OPPD10)

       RPPD11(KEEP=HOSPID BWHTCAT AGEDCAT PAGECAT SEXCAT PAYCAT RACECAT 
                   EPPD11 RPPD11 LPPD11 UPPD11 PPPD11 SPPD11 XPPD11 VPPD11 TPPD11 PPPD11 OPPD11)

       RPPD12(KEEP=HOSPID BWHTCAT AGEDCAT PAGECAT SEXCAT PAYCAT RACECAT 
                   EPPD12 RPPD12 LPPD12 UPPD12 PPPD12 SPPD12 XPPD12 VPPD12 TPPD12 PPPD12 OPPD12)
 
       RPPD70(KEEP=HOSPID BWHTCAT AGEDCAT PAGECAT SEXCAT PAYCAT RACECAT 
                   EPPD70 RPPD70 LPPD70 UPPD70 PPPD70 SPPD70 XPPD70 VPPD70 TPPD70 OPPD70
                   RENAME=(EPPD70=EPNQ01 RPPD70=RPNQ01 LPPD70=LPNQ01 UPPD70=UPNQ01
                           PPPD70=PPNQ01 SPPD70=SPNQ01 XPPD70=XPNQ01 VPPD70=VPNQ01
                           TPPD70=TPNQ01 OPPD70=OPNQ01) )
       RPPD71(KEEP=HOSPID BWHTCAT AGEDCAT PAGECAT SEXCAT PAYCAT RACECAT 
                   EPPD71 RPPD71 LPPD71 UPPD71 PPPD71 SPPD71 XPPD71 VPPD71 TPPD71 OPPD71
              RENAME=(EPPD71=EPNQ02 RPPD71=RPNQ02 LPPD71=LPNQ02 UPPD71=UPNQ02
                      PPPD71=PPNQ02 SPPD71=SPNQ02 XPPD71=XPNQ02 VPPD71=VPNQ02
                      TPPD71=TPNQ02 OPPD71=OPNQ02))

       RPPD72(KEEP=HOSPID BWHTCAT AGEDCAT PAGECAT SEXCAT PAYCAT RACECAT 
                   EPPD72 RPPD72 LPPD72 UPPD72 PPPD72 SPPD72 XPPD72 VPPD72 TPPD72 OPPD72
              RENAME=(EPPD72=EPNQ03 RPPD72=RPNQ03 LPPD72=LPNQ03 UPPD72=UPNQ03
                      PPPD72=PPNQ03 SPPD72=SPNQ03 XPPD72=XPNQ03 VPPD72=VPNQ03
                      TPPD72=TPNQ03 OPPD72=OPNQ03))
 ;
 BY HOSPID BWHTCAT AGEDCAT PAGECAT SEXCAT PAYCAT RACECAT;



 LABEL
 PPPD01 = 'PDI 01 ACCIDENTAL PUNCTURE OR LACERATION RATE (POPULATION)'
 PPPD02 = 'PDI 02 PRESSURE ULCER RATE (POPULATION)'
 PPPD05 = 'PDI 05 IATROGENIC PNEUMOTHORAX RATE (POPULATION)'
 PPPD06 = 'PDI 06 RACHS-1 PEDIATRIC HEART SURGERY MORTALITY RATE (POPULATION)'
 PPPD08 = 'PDI 08 PERIOPERATIVE HEMORRHAGE OR HEMATOMA RATE (POPULATION)'
 PPPD09 = 'PDI 09 POSTOPERATIVE RESPIRATORY FAILURE RATE (POPULATION)'
 PPPD10 = 'PDI 10 POSTOPERATIVE SEPSIS RATE (POPULATION)'
 PPPD11 = 'PDI 11 POSTOPERATIVE WOUND DEHISCENCE RATE (POPULATION)'
 PPPD12 = 'PDI 12 CENTRAL VENOUS CATHETER-RELATED BLOOD STREAM INFECTION RATE (POPULATION)'
 PPNQ01 = 'NQI 01 NEONATAL IATROGENIC PNEUMOTHORAX RATE (POPULATION)'
 PPNQ02 = 'NQI 02 NEONATAL MORTALITY RATE (POPULATION)'
 PPNQ03 = 'NQI 03 NEONATAL BLOOD STREAM INFECTION RATE (POPULATION)'
 ;

 LABEL
 OPPD01 = 'PDI 01 ACCIDENTAL PUNCTURE OR LACERATION RATE (OBSERVED)'
 OPPD02 = 'PDI 02 PRESSURE ULCER RATE (OBSERVED)'
 OPPD05 = 'PDI 05 IATROGENIC PNEUMOTHORAX RATE (OBSERVED)'
 OPPD06 = 'PDI 06 RACHS-1 PEDIATRIC HEART SURGERY MORTALITY RATE (OBSERVED)'
 OPPD08 = 'PDI 08 PERIOPERATIVE HEMORRHAGE OR HEMATOMA RATE (OBSERVED)'
 OPPD09 = 'PDI 09 POSTOPERATIVE RESPIRATORY FAILURE RATE (OBSERVED)'
 OPPD10 = 'PDI 10 POSTOPERATIVE SEPSIS RATE (OBSERVED)'
 OPPD11 = 'PDI 11 POSTOPERATIVE WOUND DEHISCENCE RATE (OBSERVED)'
 OPPD12 = 'PDI 12 CENTRAL VENOUS CATHETER-RELATED BLOOD STREAM INFECTION RATE (OBSERVED)'
 OPNQ01 = 'NQI 01 NEONATAL IATROGENIC PNEUMOTHORAX RATE (OBSERVED)'
 OPNQ02 = 'NQI 02 NEONATAL MORTALITY RATE (OBSERVED)'
 OPNQ03 = 'NQI 03 NEONATAL BLOOD STREAM INFECTION RATE (OBSERVED)'
 ;

 LABEL 
 EPPD01 = 'PDI 01 ACCIDENTAL PUNCTURE OR LACERATION RATE (EXPECTED)'
 EPPD02 = 'PDI 02 PRESSURE ULCER RATE (EXPECTED)'
 EPPD05 = 'PDI 05 IATROGENIC PNEUMOTHORAX RATE (EXPECTED)'
 EPPD06 = 'PDI 06 RACHS-1 PEDIATRIC HEART SURGERY MORTALITY RATE (EXPECTED)'
 EPPD08 = 'PDI 08 PERIOPERATIVE HEMORRHAGE OR HEMATOMA RATE (EXPECTED)'
 EPPD09 = 'PDI 09 POSTOPERATIVE RESPIRATORY FAILURE RATE (EXPECTED)'
 EPPD10 = 'PDI 10 POSTOPERATIVE SEPSIS RATE (EXPECTED)' 
 EPPD11 = 'PDI 11 POSTOPERATIVE WOUND DEHISCENCE RATE (EXPECTED)'
 EPPD12 = 'PDI 12 CENTRAL VENOUS CATHETER-RELATED BLOOD STREAM INFECTION RATE (EXPECTED)'
 EPNQ01 = 'NQI 01 NEONATAL IATROGENIC PNEUMOTHORAX RATE (EXPECTED)'
 EPNQ02 = 'NQI 02 NEONATAL MORTALITY RATE (EXPECTED)'
 EPNQ03 = 'NQI 03 NEONATAL BLOOD STREAM INFECTION RATE (EXPECTED)'
 ;

 LABEL 
 RPPD01 = 'PDI 01 ACCIDENTAL PUNCTURE OR LACERATION RATE (RISK ADJ)'
 RPPD02 = 'PDI 02 PRESSURE ULCER RATE (RISK ADJ)'
 RPPD05 = 'PDI 05 IATROGENIC PNEUMOTHORAX RATE (RISK ADJ)'
 RPPD06 = 'PDI 06 RACHS-1 PEDIATRIC HEART SURGERY MORTALITY RATE (RISK ADJ)'
 RPPD08 = 'PDI 08 PERIOPERATIVE HEMORRHAGE OR HEMATOMA RATE (RISK ADJ)'
 RPPD09 = 'PDI 09 POSTOPERATIVE RESPIRATORY FAILURE RATE (RISK ADJ)'
 RPPD10 = 'PDI 10 POSTOPERATIVE SEPSIS RATE (RISK ADJ)' 
 RPPD11 = 'PDI 11 POSTOPERATIVE WOUND DEHISCENCE RATE (RISK ADJ)'
 RPPD12 = 'PDI 12 CENTRAL VENOUS CATHETER-RELATED BLOOD STREAM INFECTION RATE (RISK ADJ)'
 RPNQ01 = 'NQI 01 NEONATAL IATROGENIC PNEUMOTHORAX RATE (RISK ADJ)'
 RPNQ02 = 'NQI 02 NEONATAL MORTALITY RATE (RISK ADJ)'
 RPNQ03 = 'NQI 03 NEONATAL BLOOD STREAM INFECTION RATE (RISK ADJ)'
 ;

 LABEL 
 LPPD01 = 'PDI 01 ACCIDENTAL PUNCTURE OR LACERATION RATE (LOWER CL)'
 LPPD02 = 'PDI 02 PRESSURE ULCER RATE (LOWER CL)'
 LPPD05 = 'PDI 05 IATROGENIC PNEUMOTHORAX RATE (LOWER CL)'
 LPPD06 = 'PDI 06 RACHS-1 PEDIATRIC HEART SURGERY MORTALITY RATE (LOWER CL)'
 LPPD08 = 'PDI 08 PERIOPERATIVE HEMORRHAGE OR HEMATOMA RATE (LOWER CL)'
 LPPD09 = 'PDI 09 POSTOPERATIVE RESPIRATORY FAILURE RATE (LOWER CL)'
 LPPD10 = 'PDI 10 POSTOPERATIVE SEPSIS RATE (LOWER CL)' 
 LPPD11 = 'PDI 11 POSTOPERATIVE WOUND DEHISCENCE RATE (LOWER CL)'
 LPPD12 = 'PDI 12 CENTRAL VENOUS CATHETER-RELATED BLOOD STREAM INFECTION RATE (LOWER CL)'
 LPNQ01 = 'NQI 01 NEONATAL IATROGENIC PNEUMOTHORAX RATE (LOWER CL)'
 LPNQ02 = 'NQI 02 NEONATAL MORTALITY RATE (LOWER CL)'
 LPNQ03 = 'NQI 03 NEONATAL BLOOD STREAM INFECTION RATE (LOWER CL)'
 ;

 LABEL 
 UPPD01 = 'PDI 01 ACCIDENTAL PUNCTURE OR LACERATION RATE (UPPER CL)'
 UPPD02 = 'PDI 02 PRESSURE ULCER RATE (UPPER CL)'
 UPPD05 = 'PDI 05 IATROGENIC PNEUMOTHORAX RATE (UPPER CL)'
 UPPD06 = 'PDI 06 RACHS-1 PEDIATRIC HEART SURGERY MORTALITY RATE (UPPER CL)'
 UPPD08 = 'PDI 08 PERIOPERATIVE HEMORRHAGE OR HEMATOMA RATE (UPPER CL)'
 UPPD09 = 'PDI 09 POSTOPERATIVE RESPIRATORY FAILURE RATE (UPPER CL)'
 UPPD10 = 'PDI 10 POSTOPERATIVE SEPSIS RATE (UPPER CL)' 
 UPPD11 = 'PDI 11 POSTOPERATIVE WOUND DEHISCENCE RATE (UPPER CL)'
 UPPD12 = 'PDI 12 CENTRAL VENOUS CATHETER-RELATED BLOOD STREAM INFECTION RATE (UPPER CL)'
 UPNQ01 = 'NQI 01 NEONATAL IATROGENIC PNEUMOTHORAX RATE (UPPER CL)'
 UPNQ02 = 'NQI 02 NEONATAL MORTALITY RATE (UPPER CL)'
 UPNQ03 = 'NQI 03 NEONATAL BLOOD STREAM INFECTION RATE (UPPER CL)'
 ;

 LABEL
 SPPD01 = 'PDI 01 ACCIDENTAL PUNCTURE OR LACERATION RATE (SMOOTHED)'
 SPPD02 = 'PDI 02 PRESSURE ULCER RATE (SMOOTHED)'
 SPPD05 = 'PDI 05 IATROGENIC PNEUMOTHORAX RATE (SMOOTHED)'
 SPPD06 = 'PDI 06 RACHS-1 PEDIATRIC HEART SURGERY MORTALITY RATE (SMOOTHED)'
 SPPD08 = 'PDI 08 PERIOPERATIVE HEMORRHAGE OR HEMATOMA RATE (SMOOTHED)'
 SPPD09 = 'PDI 09 POSTOPERATIVE RESPIRATORY FAILURE RATE (SMOOTHED)'
 SPPD10 = 'PDI 10 POSTOPERATIVE SEPSIS RATE (SMOOTHED)' 
 SPPD11 = 'PDI 11 POSTOPERATIVE WOUND DEHISCENCE RATE (SMOOTHED)'
 SPPD12 = 'PDI 12 CENTRAL VENOUS CATHETER-RELATED BLOOD STREAM INFECTION RATE (SMOOTHED)'
 SPNQ01 = 'NQI 01 NEONATAL IATROGENIC PNEUMOTHORAX RATE (SMOOTHED)'
 SPNQ02 = 'NQI 02 NEONATAL MORTALITY RATE (SMOOTHED)'
 SPNQ03 = 'NQI 03 NEONATAL BLOOD STREAM INFECTION RATE (SMOOTHED)'
 ;

 LABEL
 XPPD01 = 'PDI 01 ACCIDENTAL PUNCTURE OR LACERATION RATE (SMOOTHED SE)'
 XPPD02 = 'PDI 02 PRESSURE ULCER RATE (SMOOTHED SE)'
 XPPD05 = 'PDI 05 IATROGENIC PNEUMOTHORAX RATE (SMOOTHED SE)'
 XPPD06 = 'PDI 06 RACHS-1 PEDIATRIC HEART SURGERY MORTALITY RATE (SMOOTHED SE)'
 XPPD08 = 'PDI 08 PERIOPERATIVE HEMORRHAGE OR HEMATOMA RATE (SMOOTHED SE)'
 XPPD09 = 'PDI 09 POSTOPERATIVE RESPIRATORY FAILURE RATE (SMOOTHED SE)'
 XPPD10 = 'PDI 10 POSTOPERATIVE SEPSIS RATE (SMOOTHED SE)' 
 XPPD11 = 'PDI 11 POSTOPERATIVE WOUND DEHISCENCE RATE (SMOOTHED SE)'
 XPPD12 = 'PDI 12 CENTRAL VENOUS CATHETER-RELATED BLOOD STREAM INFECTION RATE (SMOOTHED SE)'
 XPNQ01 = 'NQI 01 NEONATAL IATROGENIC PNEUMOTHORAX RATE (SMOOTHED SE)'
 XPNQ02 = 'NQI 02 NEONATAL MORTALITY RATE (SMOOTHED SE)'
 XPNQ03 = 'NQI 03 NEONATAL BLOOD STREAM INFECTION RATE (SMOOTHED SE)'
 ;

 LABEL
 VPPD01 = 'PDI 01 ACCIDENTAL PUNCTURE OR LACERATION RATE (VARIANCE)'
 VPPD02 = 'PDI 02 PRESSURE ULCER RATE (VARIANCE)'
 VPPD05 = 'PDI 05 IATROGENIC PNEUMOTHORAX RATE (VARIANCE)'
 VPPD06 = 'PDI 06 RACHS-1 PEDIATRIC HEART SURGERY MORTALITY RATE (VARIANCE)'
 VPPD08 = 'PDI 08 PERIOPERATIVE HEMORRHAGE OR HEMATOMA RATE (VARIANCE)'
 VPPD09 = 'PDI 09 POSTOPERATIVE RESPIRATORY FAILURE RATE (VARIANCE)'
 VPPD10 = 'PDI 10 POSTOPERATIVE SEPSIS RATE (VARIANCE)'
 VPPD11 = 'PDI 11 POSTOPERATIVE WOUND DEHISCENCE RATE (VARIANCE)'
 VPPD12 = 'PDI 12 CENTRAL VENOUS CATHETER-RELATED BLOOD STREAM INFECTION RATE (VARIANCE)'
 VPNQ01 = 'NQI 01 NEONATAL IATROGENIC PNEUMOTHORAX RATE (VARIANCE)'
 VPNQ02 = 'NQI 02 NEONATAL MORTALITY RATE (VARIANCE)'
 VPNQ03 = 'NQI 03 NEONATAL BLOOD STREAM INFECTION RATE (VARIANCE)'
 ;

 * -------------------------------------------------------------- ;
 * --- RE-LABEL DAY DEPENDENT INDICATORS ------------------------ ;
 * -------------------------------------------------------------- ;
 %MACRO RELABEL;

 %IF &PRDAY. = 0 %THEN %DO;

 LABEL 
 EPPD02 = 'PDI 02 PRESSURE ULCER RATE-NO PRDAY (EXPECTED)'
 EPPD08 = 'PDI 08 PERIOPERATIVE HEMORRHAGE OR HEMATOMA RATE-NO PRDAY (EXPECTED)'
 EPPD09 = 'PDI 09 POSTOPERATIVE RESPIRATORY FAILURE RATE-NO PRDAY (EXPECTED)'
 EPPD11 = 'PDI 11 POSTOPERATIVE WOUND DEHISCENCE RATE-NO PRDAY (EXPECTED)'
 ;

 LABEL 
 RPPD02 = 'PDI 02 PRESSURE ULCER RATE-NO PRDAY (RISK ADJ)'
 RPPD08 = 'PDI 08 PERIOPERATIVE HEMORRHAGE OR HEMATOMA RATE-NO PRDAY (RISK ADJ)'
 RPPD09 = 'PDI 09 POSTOPERATIVE RESPIRATORY FAILURE RATE-NO PRDAY (RISK ADJ)'
 RPPD11 = 'PDI 11 POSTOPERATIVE WOUND DEHISCENCE RATE-NO PRDAY (RISK ADJ)'
 ;

 LABEL
 SPPD02 = 'PDI 02 PRESSURE ULCER RATE-NO PRDAY (SMOOTHED)'
 SPPD08 = 'PDI 08 PERIOPERATIVE HEMORRHAGE OR HEMATOMA RATE-NO PRDAY (SMOOTHED)'
 SPPD09 = 'PDI 09 POSTOPERATIVE RESPIRATORY FAILURE RATE-NO PRDAY (SMOOTHED)'
 SPPD11 = 'PDI 11 POSTOPERATIVE WOUND DEHISCENCE RATE-NO PRDAY (SMOOTHED)'
 ;
 
 %END;
 %MEND RELABEL;
 %RELABEL;

 RUN;

 *==================================================================;
 *  TITLE:  PROGRAM P3  PART II:  MERGE PROVIDER RATES FOR AHRQ
 *  PEDIATRIC QUALITY INDICATORS
 *
 *  DESCRIPTION:  MERGED RATES FOR PEDIATRIC QUALITY INDICATORS
 *
 *          >>>  VERSION 5.0, DECEMBER 2014  <<<
 *
 *===================================================================;

 TITLE2 'PROGRAM P3 PART II';
 TITLE3 'AHRQ PEDIATRIC QUALITY INDICATORS: PROVIDER-LEVEL MERGED FILES';

 * ---------------------------------------------------------------- ;
 * --- PEDIATRIC QUALITY INDICATOR MERGED RATES                   - ;
 * ---------------------------------------------------------------- ;

  
 DATA &OUTFILP3.;
 MERGE 
 INP2.&INFILEP2.(
    KEEP=HOSPID BWHTCAT AGEDCAT PAGECAT SEXCAT PAYCAT RACECAT _TYPE_ 
         TPPD03 TPPD07 TPPD13  
         PPPD03 PPPD13  
         OPPD03 OPPD13 )

 RISKADJ(
    KEEP=HOSPID BWHTCAT AGEDCAT PAGECAT SEXCAT PAYCAT RACECAT  
         EPPD01 EPPD02 EPPD05-EPPD06 EPPD08-EPPD12 EPNQ01-EPNQ03 
         RPPD01 RPPD02 RPPD05-RPPD06 RPPD08-RPPD12 RPNQ01-RPNQ03 
         LPPD01 LPPD02 LPPD05-LPPD06 LPPD08-LPPD12 LPNQ01-LPNQ03 
         UPPD01 UPPD02 UPPD05-UPPD06 UPPD08-UPPD12 UPNQ01-UPNQ03 
         SPPD01 SPPD02 SPPD05-SPPD06 SPPD08-SPPD12 SPNQ01-SPNQ03 
         XPPD01 XPPD02 XPPD05-XPPD06 XPPD08-XPPD12 XPNQ01-XPNQ03 
         VPPD01 VPPD02 VPPD05-VPPD06 VPPD08-VPPD12 VPNQ01-VPNQ03
         TPPD01 TPPD02 TPPD05-TPPD06 TPPD08-TPPD12 TPNQ01-TPNQ03
         PPPD01 PPPD02 PPPD05-PPPD06 PPPD08-PPPD12 PPNQ01-PPNQ03
         OPPD01 OPPD02 OPPD05-OPPD06 OPPD08-OPPD12 OPNQ01-OPNQ03);
 BY HOSPID BWHTCAT AGEDCAT PAGECAT SEXCAT PAYCAT RACECAT;

 %INCLUDE MSXP;


 ARRAY ARRY1{12} EPPD01 EPPD02 EPPD05-EPPD06 EPPD08-EPPD12 EPNQ01-EPNQ03 ;
 ARRAY ARRY2{12} RPPD01 RPPD02 RPPD05-RPPD06 RPPD08-RPPD12 RPNQ01-RPNQ03 ;
 ARRAY ARRY3{12} LPPD01 LPPD02 LPPD05-LPPD06 LPPD08-LPPD12 LPNQ01-LPNQ03 ;
 ARRAY ARRY4{12} UPPD01 UPPD02 UPPD05-UPPD06 UPPD08-UPPD12 UPNQ01-UPNQ03 ;
 ARRAY ARRY5{12} SPPD01 SPPD02 SPPD05-SPPD06 SPPD08-SPPD12 SPNQ01-SPNQ03 ;
 ARRAY ARRY6{12} XPPD01 XPPD02 XPPD05-XPPD06 XPPD08-XPPD12 XPNQ01-XPNQ03 ;
 ARRAY ARRY7{12} VPPD01 VPPD02 VPPD05-VPPD06 VPPD08-VPPD12 VPNQ01-VPNQ03 ;
 ARRAY ARRY8{12} PPPD01 PPPD02 PPPD05-PPPD06 PPPD08-PPPD12 PPNQ01-PPNQ03 ;

 DO I = 1 TO 12;
    IF ARRY8(I) <= 2 THEN DO;
       ARRY1(I) = .; 
       ARRY2(I) = .; 
       ARRY3(I) = .; 
       ARRY4(I) = .;
       ARRY5(I) = .; 
       ARRY6(I) = .; 
       ARRY7(I) = .; 
    END;
 END;

 DROP I;

 FORMAT EPNQ01 EPNQ02 EPNQ03 
        EPPD01 EPPD02 EPPD05 EPPD06 EPPD08 EPPD09 EPPD10 EPPD11 EPPD12 
        
         LPNQ01 LPNQ02 LPNQ03 
        LPPD01 LPPD02 LPPD05 LPPD06 LPPD08 LPPD09 LPPD10 LPPD11 LPPD12 
        
        OPNQ01 OPNQ02 OPNQ03 
         OPPD01 OPPD02 OPPD03 OPPD05 OPPD06 OPPD08 OPPD09 OPPD10 OPPD11 OPPD12 OPPD13 
    
        RPNQ01 RPNQ02 RPNQ03 
        RPPD01 RPPD02 RPPD05 RPPD06 RPPD08 RPPD09 RPPD10 RPPD11 RPPD12 
        
        SPNQ01 SPNQ02 SPNQ03 
        SPPD01 SPPD02 SPPD05 SPPD06 SPPD08 SPPD09 SPPD10 SPPD11 SPPD12
        
        UPNQ01 UPNQ02 UPNQ03 
        UPPD01 UPPD02 UPPD05 UPPD06 UPPD08 UPPD09 UPPD10 UPPD11 UPPD12
    
        VPNQ01 VPNQ02 VPNQ03
        VPPD01 VPPD02 VPPD05 VPPD06 VPPD08 VPPD09 VPPD10 VPPD11 VPPD12
    
        XPNQ01 XPNQ02 XPNQ03 
        XPPD01 XPPD02 XPPD05 XPPD06 XPPD08 XPPD09 XPPD10 XPPD11 XPPD12
        13.7
        TPNQ01 TPNQ02 TPNQ03
        TPPD01 TPPD02 TPPD03 TPPD05 TPPD06 TPPD07 TPPD08 TPPD09 TPPD10 TPPD11 TPPD12 TPPD13
        
        PPNQ01 PPNQ02 PPNQ03
        PPPD01 PPPD02 PPPD03 PPPD05 PPPD06 PPPD08 PPPD09 PPPD10 PPPD11 PPPD12 PPPD13
         13.0;
 RUN;

 
 DATA OUTP3.&OUTFILP3.;
   SET &OUTFILP3.;
 RUN;
 * -------------------------------------------------------------- ;
 * --- CONTENTS AND MEANS OF PROVIDER-LEVEL MERGED FILE --------- ;
 * -------------------------------------------------------------- ;

 PROC CONTENTS DATA=OUTP3.&OUTFILP3. POSITION;
 RUN;

 PROC MEANS DATA=OUTP3.&OUTFILP3.
   /*(WHERE=(_TYPE_ IN (64)))*/ N NMISS MIN MAX MEAN SUM NOLABELS;
 TITLE4 "SUMMARY OF PROVIDER-LEVEL RATES (_TYPE_=64)";
 RUN;

 * -------------------------------------------------------------- ;
 * --- PRINT PROVIDER MERGED FILE ------------------------------- ;
 * -------------------------------------------------------------- ;
%LET PRINT = 1;
 %MACRO PRT2;

 %IF &PRINT. = 1 %THEN %DO;

 %MACRO PRT(PD,TEXT,CASE);

 PROC PRINT DATA=OUTP3.&OUTFILP3. LABEL SPLIT="*";
 %IF &CASE. = 2 %THEN %DO;
 VAR HOSPID BWHTCAT AGEDCAT PAGECAT SEXCAT PAYCAT RACECAT 
     TP&PD.;
 LABEL HOSPID  = "HOSPID"
       BWHTCAT = "BWHTCAT"
       AGEDCAT = "AGEDCAT"
       PAGECAT = "PAGECAT"
       SEXCAT  = "SEXCAT"
       PAYCAT  = "PAYCAT"
       RACECAT = "RACECAT"
       TP&PD. = "TP&PD.*(VOLUME)"
 ;
 FORMAT BWHTCAT BWHTCAT.
        AGEDCAT AGEDCAT.
        PAGECAT PAGECAT.
        SEXCAT SEXCAT.
        PAYCAT PAYCAT.
        RACECAT RACECAT.
        TP&PD. 13.0;
 %END;
 %ELSE %IF &CASE. = 1 %THEN %DO;
 VAR HOSPID BWHTCAT AGEDCAT PAGECAT SEXCAT PAYCAT RACECAT
     TP&PD. PP&PD. OP&PD.;
 LABEL HOSPID  = "HOSPID"
       BWHTCAT = "BWHTCAT"
       AGEDCAT = "AGEDCAT"
       PAGECAT = "PAGECAT"
       SEXCAT  = "SEXCAT"
       PAYCAT  = "PAYCAT"
       RACECAT = "RACECAT"
       TP&PD. = "TP&PD.*(NUMERATOR)"
       PP&PD. = "PP&PD.*(DENOMINATOR)"
       OP&PD. = "OP&PD.*(OBSERVED)"
 ;
 FORMAT BWHTCAT BWHTCAT.
        AGEDCAT AGEDCAT.
        PAGECAT PAGECAT.
        SEXCAT SEXCAT.
        PAYCAT PAYCAT.
        RACECAT RACECAT.
        TP&PD. PP&PD. 13.0 OP&PD. 8.6;
 %END;
 %ELSE %DO;
 VAR HOSPID BWHTCAT AGEDCAT PAGECAT SEXCAT PAYCAT RACECAT
     TP&PD. PP&PD. OP&PD. EP&PD. RP&PD. LP&PD. UP&PD. SP&PD. XP&PD.;
 LABEL HOSPID  = "HOSPID"
       BWHTCAT = "BWHTCAT"
       AGEDCAT = "AGEDCAT"
       PAGECAT = "PAGECAT"
       SEXCAT  = "SEXCAT"
       PAYCAT  = "PAYCAT"
       RACECAT = "RACECAT"
       TP&PD. = "TP&PD.*(NUMERATOR)"
       PP&PD. = "PP&PD.*(DENOMINATOR)"
       OP&PD. = "OP&PD.*(OBSERVED)"
       EP&PD. = "EP&PD.*(EXPECTED)"
       RP&PD. = "RP&PD.*(RISK ADJ)"
       LP&PD. = "LP&PD.*(LOWER CL)"
       UP&PD. = "UP&PD.*(UPPER CL)"
       SP&PD. = "SP&PD.*(SMOOTHED)"
       XP&PD. = "XP&PD.*(SMOOTHED SE)"
 ;
 FORMAT BWHTCAT BWHTCAT.
        AGEDCAT AGEDCAT.
        PAGECAT PAGECAT.
        SEXCAT SEXCAT.
        PAYCAT PAYCAT.
        RACECAT RACECAT.
        TP&PD. PP&PD. 13.0 OP&PD. RP&PD. SP&PD. EP&PD. 8.6;
 %END;
 TITLE4 "FINAL OUTPUT";
 TITLE5 "INDICATOR &PD.: &TEXT";

 RUN;

 %MEND PRT;

 %PRT(PD01,ACCIDENTAL PUNCTURE OR LACERATION RATE,0);
 %PRT(PD02,PRESSURE ULCER RATE,0);
 %PRT(PD03,RETAINED SURGICAL ITEM OR UNRETRIEVED DEVICE FRAGMENT COUNT,1);
 %PRT(PD05,IATROGENIC PNEUMOTHORAX RATE,0);
 %PRT(PD06,RACHS-1 PEDIATRIC HEART SURGERY MORTALITY RATE,0);
 %PRT(PD07,RACHS-1 PEDIATRIC HEART SURGERY VOLUME,2);
 %PRT(PD08,PERIOPERATIVE HEMORRHAGE OR HEMATOMA RATE,0);
 %PRT(PD09,POSTOPERATIVE RESPIRATORY FAILURE RATE,0);
 %PRT(PD10,POSTOPERATIVE SEPSIS RATE,0);
 %PRT(PD11,POSTOPERATIVE WOUND DEHISCENCE RATE,0);
 %PRT(PD12,CENTRAL VENOUS CATHETER-RELATED BLOOD STREAM INFECTION RATE,0);
 %PRT(PD13,TRANSFUSION REACTION COUNT,1);
 %PRT(NQ01,NEONATAL IATROGENIC PNEUMOTHORAX RATE,0);
 %PRT(NQ02,NEONATAL MORTALITY RATE,0);
 %PRT(NQ03,NEONATAL BLOOD STREAM INFECTION RATE,0);

 %END;

 %MEND PRT2;

 %PRT2;

 * -------------------------------------------------------------- ;
 * --- WRITE SAS OUTPUT DATA SET TO COMMA-DELIMITED TEXT FILE --- ;
 * --- FOR EXPORT INTO SPREADSHEETS ----------------------------- ;
 * -------------------------------------------------------------- ;

 %MACRO TEXT;

 %IF &TEXTP3. = 1  %THEN %DO;

 DATA _NULL_;
 SET OUTP3.&OUTFILP3;
 FILE PDTEXTP3 LRECL=4000;
 IF _N_=1 THEN PUT
 "HOSP ID" "," "BWHT" "," "AGED" "," "AGE"  "," 
 "SEX" "," "PAYER" "," "RACE"  "," "TYPE" ","
 "TPPD01" "," "TPPD02" "," "TPPD03" "," 
 "TPPD05" "," "TPPD06" "," "TPPD07" "," "TPPD08" ","
 "TPPD09" "," "TPPD10" "," "TPPD11" "," "TPPD12" ","
 "TPPD13" "," "TPNQ01" "," "TPNQ02" "," "TPNQ03" ","

 "PPPD01" "," "PPPD02" "," "TPPD03" "," 
 "PPPD05" "," "PPPD06" "," "PPPD08" ","
 "PPPD09" "," "PPPD10" "," "PPPD11" "," "PPPD12" ","
 "PPNQ01" "," "PPNQ02" "," "PPNQ03" ","
 "OPPD01" "," "OPPD02" "," "OPPD03" "," 
 "OPPD05" "," "OPPD06" "," "OPPD08" ","
 "OPPD09" "," "OPPD10" "," "OPPD11" "," "OPPD12" ","
 "OPNQ01" "," "OPNQ02" "," "OPNQ03" ","

 "EPPD01" "," "EPPD02" ","   
 "EPPD05" "," "EPPD06" "," "EPPD08" ","
 "EPPD09" "," "EPPD10" "," "EPPD11" "," "EPPD12" ","
 "EPNQ01" "," "EPNQ02" "," "EPNQ03" "," 
 "RPPD01" "," "RPPD02" ","   
 "RPPD05" "," "RPPD06" "," "RPPD08" ","
 "RPPD09" "," "RPPD10" "," "RPPD11" "," "RPPD12" ","
 "RPNQ01" "," "RPNQ02" "," "RPNQ03" "," 
 "LPPD01" "," "LPPD02" ","   
 "LPPD05" "," "LPPD06" "," "LPPD08" ","
 "LPPD09" "," "LPPD10" "," "LPPD11" "," "LPPD12" ","
 "LPNQ01" "," "LPNQ02" "," "LPNQ03" "," 
 "UPPD01" "," "UPPD02" ","   
 "UPPD05" "," "UPPD06" "," "UPPD08" ","
 "UPPD09" "," "UPPD10" "," "UPPD11" "," "UPPD12" ","
 "UPNQ01" "," "UPNQ02" "," "UPNQ03" "," 
 "SPPD01" "," "SPPD02" ","   
 "SPPD05" "," "SPPD06" "," "SPPD08" ","
 "SPPD09" "," "SPPD10" "," "SPPD11" "," "SPPD12" ","
 "SPNQ01" "," "SPNQ02" "," "SPNQ03" "," 
 "XPPD01" "," "XPPD02" ","   
 "XPPD05" "," "XPPD06" "," "XPPD08" ","
 "XPPD09" "," "XPPD10" "," "XPPD11" "," "XPPD12" ","
 "XPNQ01" "," "XPNQ02" "," "XPNQ03" "," 
 ;
 PUT HOSPID 13. "," BWHTCAT 3. "," AGEDCAT 3. "," PAGECAT 3. "," 
     SEXCAT 3. "," PAYCAT 3. "," RACECAT 3. "," _TYPE_ 2. ","
 (TPPD01-TPPD03 TPPD05-TPPD13 TPNQ01-TPNQ03 ) (7.0 ",") "," 
 (PPPD01-PPPD03 PPPD05-PPPD06 PPPD08-PPPD12 PPNQ01-PPNQ03 ) (13.2 ",") ","
 (OPPD01-OPPD03 OPPD05-OPPD06 OPPD08-OPPD12 OPNQ01-OPNQ03 ) (12.10 ",") ","
 (EPPD01 EPPD02 EPPD05-EPPD06 EPPD08-EPPD12 EPNQ01-EPNQ03 ) (12.10 ",") ","
 (RPPD01 RPPD02 RPPD05-RPPD06 RPPD08-RPPD12 RPNQ01-RPNQ03 ) (12.10 ",") ","
 (LPPD01 LPPD02 LPPD05-LPPD06 LPPD08-LPPD12 LPNQ01-LPNQ03 ) (12.10 ",") ","
 (UPPD01 UPPD02 UPPD05-UPPD06 UPPD08-UPPD12 UPNQ01-UPNQ03 ) (12.10 ",") ","
 (SPPD01 SPPD02 SPPD05-SPPD06 SPPD08-SPPD12 SPNQ01-SPNQ03 ) (12.10 ",") ","
 (XPPD01 XPPD02 XPPD05-XPPD06 XPPD08-XPPD12 XPNQ01-XPNQ03 ) (12.10 ",");

 RUN;

 %END;

 %MEND TEXT;

 %TEXT;
