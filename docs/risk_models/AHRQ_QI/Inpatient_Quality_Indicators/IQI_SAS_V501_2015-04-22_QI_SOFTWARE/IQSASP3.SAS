*================================================================================================;
*  TITLE:  PROGRAM P3: CALCULATES RISK-ADJUSTED PROVIDER
*          RATES FOR AHRQ INPATIENT QUALITY INDICATORS
*
*  DESCRIPTION:
*         USES PROC SCORE TO CALCULATE RISK-ADJUSTED RATES FOR
*         INPATIENT QUALITY INDICATORS.
*         ADJUSTS FOR: SEX, AGE, APR-DRG SUBCLASS AND MDC
*
*             >>>  VERSION 5.0.1 - APRIL 22, 2015  <<<
*
*  USER NOTE: THE AHRQ QI SAS SOFTWARE DOES NOT EXPLICITLY SUPPORT THE CALCULATION 
*      OF WEIGHTED ESTIMATES AND STANDARD ERRORS USING COMPLEX SAMPLING DESIGNS.
*       ALL REFERENCES TO THE VARIABLE DISCWT HAVE BEEN REMOVED FROM THE QI SAS SOFTWARE
*			  TO OBTAIN WEIGHTED NATIONALLY REPRESENTATIVE ESTIMATES, PLEASE REFER TO THE TECHNICAL 
*			  DOCUMENTATION ON THE AHRQ QI WEBSITE. HTTP://WWW.QUALITYINDICATORS.AHRQ.GOV/ */
*=================================================================================================;
FILENAME CONTROL 'C:\PATHNAME\CONTROL_IQI.SAS';  *<===USER MUST modify; 

%INCLUDE CONTROL;

TITLE2 'PROGRAM P3 PART I';
TITLE3 'AHRQ INPATIENT QUALITY INDICATORS: CALCULATE RISK-ADJUSTED PROVIDER RATES';


 * -------------------------------------------------------------- ;
 * --- INPATIENT QUALITY INDICATOR ADJUSTED RATES  -------------- ;
 * -------------------------------------------------------------- ;
 * --- THIS STEP SELECTS THE OBSERVATIONS FROM THE PROGRAM 1    - ;
 * --- OUTPUT FOR EACH INPATIENT QUALITY INDICATOR IN TURN.     - ;
 * --- EACH ITERATION PASSES VARIABLES THAT CONTROL THE         - ;
 * --- COVARIATES FOR THAT INPATIENT QUALITY INDICATOR:         - ;
 * ---                                                          - ;
 * --- IQ - THE INPATIENT QUALITY INDICATOR NAME WITHOUT THE    - ;
 * ---      PREFIX (P)                                          - ;
 * ---          QUALITY INDICATOR                               - ;
 * --- CV - NUMBER OF COVARIATES                               - ;
 * -------------------------------------------------------------- ;

*---------------------------------------------------------------------*;
*-- MAKCOVAR CREATES THE SETS OF VARIABLES USED FOR RISK ADJUSTMENT --*;
*-- THE USER MUST ENSURE THAT THE CONTROL FILE INCLUDES THE CORRECT --*;
*-- LOCATION FOR THE MAKCOVAR SAS PROGRAM.                          --*;
*---------------------------------------------------------------------*;

%INCLUDE MAKCOVAR;

%MACRO MOD3(IQ, CV);
 
 DATA   TEMP1;

 SET    IN1.&INFILE1.;

/* DROP PROVIDER LEVEL INDICATORS THAT ARE NOT RISK-ADJUSTED */

 DROP TPIQ21-TPIQ25 TPIQ33 TPIQ34;
 				 *PPIQ21-PPIQ25 PPIQ33 PPIQ34;
 			*OPIQ21-OPIQ25 OPIQ33 OPIQ34;

 *--  DATA STEP CODE TO CREATE RISK ADJUSTMENT COVARIATES --*;

 %MAKEVARS_IQI_&IQ. ;

 RUN;                          

*-- CHOSE PARAMETER FILE USED FOR SCORING --*;

%IF &USEPOA=1 %THEN %DO ;

FILENAME RACOEFFS  "&RADIR.\QI50_IQI_&IQ._POA.CSV";

%END ;

%IF &USEPOA=0 %THEN %DO ;
FILENAME RACOEFFS  "&RADIR.\QI50_IQI_&IQ._NOPOA.CSV";
%END ;

*-- LOAD CSV PARAMTERS & SHAPE DATA  --*;
DATA TEMP1_MODEL ;

LENGTH VARIABLE $10 DF ESTIMATE 8 ;
  INFILE RACOEFFS DSD DLM=',' LRECL=1024;
  INPUT VARIABLE DF ESTIMATE ;
RUN ;


PROC TRANSPOSE DATA=TEMP1_MODEL OUT=TEMP2_MODEL  ;
VAR ESTIMATE ;
RUN ;


DATA MODEL_QI&IQ. (KEEP=INTERCEPT XCV1-XCV&CV. _NAME_ _TYPE_) ; 
  SET TEMP2_MODEL  ;

  RENAME COL1=INTERCEPT ;
  %DO I_=1 %TO &CV. ;
  RENAME COL%EVAL(&I_ + 1) = XCV&I_ ;
  %END ;

  _NAME_ = "MHAT" ;
  _TYPE_ = "PARMS" ;

RUN ;

*-- APPLY PROC SCORE TO DATA --* ;                   

 PROC   SCORE DATA=TEMP1 SCORE=MODEL_QI&IQ. TYPE=PARMS OUT=TEMP1Y;
 VAR    XCV1-XCV&CV. ;
 RUN;

 
 %LET DSID=%SYSFUNC(OPEN(TEMP1Y));
 %LET DNUM=%SYSFUNC(ATTRN(&DSID,NOBS));
 %LET DRC=%SYSFUNC(CLOSE(&DSID));

 %IF &DNUM NE 0 %THEN %DO;

 *-- CALCULATE PREDICTED VALUES (EHAT) --* ;
 DATA   TEMP1Y;
 SET    TEMP1Y;

 EHAT = EXP(MHAT)/(1 + EXP(MHAT));
 PHAT = EHAT * (1 - EHAT);

 RUN;

 *-- SUMMARIZE BY VARIOUS CLASSES --*;
 PROC   SUMMARY DATA=TEMP1Y;
 CLASS  HOSPID AGECAT SEXCAT PAYCAT RACECAT;
 VAR    TPIQ&IQ. EHAT PHAT;
 OUTPUT OUT=RPIQ&IQ. SUM(TPIQ&IQ. EHAT PHAT)=TPIQ&IQ. EHAT PHAT
                     SUMWGT(TPIQ&IQ.)=PPIQ&IQ.
                     N=DENOM;
 RUN;


 *-- APPLY RISK ADJUSTMENT & SMOOTHING --* ;

 DATA   RPIQ&IQ.(KEEP=HOSPID AGECAT SEXCAT PAYCAT RACECAT _TYPE_
                 EPIQ&IQ. RPIQ&IQ. LPIQ&IQ. UPIQ&IQ. SPIQ&IQ. XPIQ&IQ. VPIQ&IQ.
				 TPIQ&IQ. PPIQ&IQ. OPIQ&IQ. 
                 );
 SET    RPIQ&IQ.;

 IF _TYPE_ &TYPELVLP;

 *-- LOAD SIGNALV VARIANCE AND POPULATION REFERENCE RATE ARRAYS--* ;

 %INCLUDE MSXP;

 *-- MAP MEASURE NUM TO ARRAY INDEX SUB_N --* ;
 IF "&IQ." ="08"  THEN SUB_N = 1;

 IF "&IQ." = "09"  THEN SUB_N = 2;
 IF "&IQ." = "09A" THEN SUB_N = 3;
 IF "&IQ." = "09B" THEN SUB_N = 4;

 IF "&IQ." = "11"  THEN SUB_N = 5;
 IF "&IQ." = "11A" THEN SUB_N = 6;
 IF "&IQ." = "11B" THEN SUB_N = 7;
 IF "&IQ." = "11C" THEN SUB_N = 8;
 IF "&IQ." = "11D" THEN SUB_N = 9;

 IF "&IQ." = "12"  THEN SUB_N = 10;
 IF "&IQ." = "13"  THEN SUB_N = 11;
 IF "&IQ." = "14"  THEN SUB_N = 12;
 IF "&IQ." = "15"  THEN SUB_N = 13;
 IF "&IQ." = "16"  THEN SUB_N = 14;

 IF "&IQ." = "17"  THEN SUB_N = 15;
 IF "&IQ." = "17A" THEN SUB_N = 16;
 IF "&IQ." = "17B" THEN SUB_N = 17;
 IF "&IQ." = "17C" THEN SUB_N = 18;

 IF "&IQ." = "18"  THEN SUB_N = 19;
 IF "&IQ." = "19"  THEN SUB_N = 20;
 IF "&IQ." = "20"  THEN SUB_N = 21;
 IF "&IQ." = "30"  THEN SUB_N = 27;
 IF "&IQ." = "31"  THEN SUB_N = 28;
 IF "&IQ." = "32"  THEN SUB_N = 29;

 *-- T = NUMERATOR     --* ;
 *-- P = DENOMINATOR   --* ;
 *-- E = EXPECTED      --* ;
 *-- R = RISK ADJUSTED --* ; 
 *-- L = LOWER CI      --* ;
 *-- U = UPPER CI      --* ;
 *-- S = SMOOTHED      --* ;
 *-- X = SMOOTHED SE   --* ;
 *-- V = VARIANCE      --* ; 

 EPIQ&IQ. = EHAT / PPIQ&IQ.;
 THAT = TPIQ&IQ. / PPIQ&IQ.;
 OPIQ&IQ. = TPIQ&IQ. / PPIQ&IQ.;
 
 
 IF _TYPE_ IN (0,16) THEN DO;
    RPIQ&IQ.   = (THAT / EPIQ&IQ.) * ARRYP3(SUB_N);
    SE&IQ.  = (ARRYP3(SUB_N) / EPIQ&IQ.) * (1 / PPIQ&IQ.) * SQRT(PHAT);
    VPIQ&IQ.   = SE&IQ.**2;
    SN&IQ.  = ARRYP2(SUB_N) / (ARRYP2(SUB_N) +  VPIQ&IQ. );
    SPIQ&IQ.   = (RPIQ&IQ. * SN&IQ.) + ((1 -  SN&IQ.) * ARRYP3(SUB_N));
    XPIQ&IQ.   = SQRT(ARRYP2(SUB_N)- (SN&IQ. * ARRYP2(SUB_N)));
 END;
 ELSE DO;
    RPIQ&IQ.   = (THAT / EPIQ&IQ.);
    SE&IQ.  = (1 / EPIQ&IQ.) * (1 / PPIQ&IQ.) * SQRT(PHAT);
    VPIQ&IQ.   = .;
    SPIQ&IQ.   = .;
    XPIQ&IQ.   = .;
 END;
 
 LPIQ&IQ.   = RPIQ&IQ. - (1.96 * SE&IQ.);
 IF LPIQ&IQ. < 0 THEN LPIQ&IQ. = 0;
 UPIQ&IQ.   = RPIQ&IQ. + (1.96 * SE&IQ.);

 RUN;

 %END;

 %ELSE %DO;

 DATA   RPIQ&IQ.;
    HOSPID=.;AGECAT=.;SEXCAT=.;RACECAT=.;PAYCAT=.;_TYPE_=0;
    EPIQ&IQ=.;RPIQ&IQ=.;LPIQ&IQ=.;UPIQ&IQ=.;SPIQ&IQ=.;XPIQ&IQ=.;VPIQ&IQ=.;
    TPIQ&IQ=. ; PPIQ&IQ=. ; OPIQ&IQ.=.; 
    OUTPUT;
 RUN;

 %END;

 DATA RPIQ&IQ.;
   SET RPIQ&IQ.;
   IF "&IQ." IN ("09A", "09B", "11A" , "11B", "11C", "11D", 
                "17A", "17B", "17C")  THEN DO;
      SPIQ&IQ. = .;
      XPIQ&IQ. = .;
   END;
 RUN; 

 PROC SORT DATA=RPIQ&IQ.;
   BY HOSPID AGECAT SEXCAT PAYCAT RACECAT;
 RUN; QUIT;
   
 PROC   DATASETS NOLIST;
 DELETE TEMP1 TEMP1Y TEMP1_MODEL TEMP2_MODEL;
 RUN;


 %MEND MOD3;

%MOD3(08,5) ;
%MOD3(09,6) ;
%MOD3(09A,6) ;
%MOD3(09B,6) ;
%MOD3(11,11) ;
%MOD3(11A,11) ;
%MOD3(11B,11) ;
%MOD3(11C,11) ;
%MOD3(11D,11) ;
%MOD3(12,23) ;
%MOD3(13,16) ;
%MOD3(14,6) ;
%MOD3(15,26) ;
%MOD3(16,28) ;
%MOD3(17,29) ;
%MOD3(17A,29) ;
%MOD3(17B,29) ;
%MOD3(17C,29) ;
%MOD3(18,24) ;
%MOD3(19,17) ;
%MOD3(20,28) ;
%MOD3(30,21) ;
%MOD3(31,6) ;
%MOD3(32,23) ;


 * --- MERGES THE AREA ADJUSTED RATES FOR EACH INPATIENT QUALITY  - ;
 * --- INDICATOR.  PREFIX FOR THE ADJUSTED RATES IS (R).          - ;


 DATA   RISKADJ;
 MERGE  RPIQ08 (KEEP=HOSPID AGECAT SEXCAT PAYCAT RACECAT EPIQ08 RPIQ08 LPIQ08 UPIQ08 SPIQ08 XPIQ08 VPIQ08 TPIQ08 PPIQ08 OPIQ08)

        RPIQ09 (KEEP=HOSPID AGECAT SEXCAT PAYCAT RACECAT EPIQ09  RPIQ09  LPIQ09  UPIQ09  SPIQ09  XPIQ09  VPIQ09  TPIQ09  PPIQ09  OPIQ09)
        RPIQ09A(KEEP=HOSPID AGECAT SEXCAT PAYCAT RACECAT EPIQ09A RPIQ09A LPIQ09A UPIQ09A SPIQ09A XPIQ09A VPIQ09A TPIQ09A PPIQ09A OPIQ09A)
        RPIQ09B(KEEP=HOSPID AGECAT SEXCAT PAYCAT RACECAT EPIQ09B RPIQ09B LPIQ09B UPIQ09B SPIQ09B XPIQ09B VPIQ09B TPIQ09B PPIQ09B OPIQ09B)

        RPIQ11 (KEEP=HOSPID AGECAT SEXCAT PAYCAT RACECAT EPIQ11  RPIQ11  LPIQ11  UPIQ11  SPIQ11  XPIQ11  VPIQ11  TPIQ11  PPIQ11  OPIQ11) 
        RPIQ11A(KEEP=HOSPID AGECAT SEXCAT PAYCAT RACECAT EPIQ11A RPIQ11A LPIQ11A UPIQ11A SPIQ11A XPIQ11A VPIQ11A TPIQ11A PPIQ11A OPIQ11A)
        RPIQ11B(KEEP=HOSPID AGECAT SEXCAT PAYCAT RACECAT EPIQ11B RPIQ11B LPIQ11B UPIQ11B SPIQ11B XPIQ11B VPIQ11B TPIQ11B PPIQ11B OPIQ11B)
        RPIQ11C(KEEP=HOSPID AGECAT SEXCAT PAYCAT RACECAT EPIQ11C RPIQ11C LPIQ11C UPIQ11C SPIQ11C XPIQ11C VPIQ11C TPIQ11C PPIQ11C OPIQ11C)
        RPIQ11D(KEEP=HOSPID AGECAT SEXCAT PAYCAT RACECAT EPIQ11D RPIQ11D LPIQ11D UPIQ11D SPIQ11D XPIQ11D VPIQ11D TPIQ11D PPIQ11D OPIQ11D)

        RPIQ12(KEEP=HOSPID AGECAT SEXCAT PAYCAT RACECAT EPIQ12 RPIQ12 LPIQ12 UPIQ12 SPIQ12 XPIQ12 VPIQ12 TPIQ12 PPIQ12 OPIQ12)
        RPIQ13(KEEP=HOSPID AGECAT SEXCAT PAYCAT RACECAT EPIQ13 RPIQ13 LPIQ13 UPIQ13 SPIQ13 XPIQ13 VPIQ13 TPIQ13 PPIQ13 OPIQ13)
        RPIQ14(KEEP=HOSPID AGECAT SEXCAT PAYCAT RACECAT EPIQ14 RPIQ14 LPIQ14 UPIQ14 SPIQ14 XPIQ14 VPIQ14 TPIQ14 PPIQ14 OPIQ14)
        RPIQ15(KEEP=HOSPID AGECAT SEXCAT PAYCAT RACECAT EPIQ15 RPIQ15 LPIQ15 UPIQ15 SPIQ15 XPIQ15 VPIQ15 TPIQ15 PPIQ15 OPIQ15)
        RPIQ16(KEEP=HOSPID AGECAT SEXCAT PAYCAT RACECAT EPIQ16 RPIQ16 LPIQ16 UPIQ16 SPIQ16 XPIQ16 VPIQ16 TPIQ16 PPIQ16 OPIQ16)

        RPIQ17 (KEEP=HOSPID AGECAT SEXCAT PAYCAT RACECAT EPIQ17  RPIQ17  LPIQ17  UPIQ17  SPIQ17  XPIQ17  VPIQ17  TPIQ17  PPIQ17  OPIQ17)
        RPIQ17A(KEEP=HOSPID AGECAT SEXCAT PAYCAT RACECAT EPIQ17A RPIQ17A LPIQ17A UPIQ17A SPIQ17A XPIQ17A VPIQ17A TPIQ17A PPIQ17A OPIQ17A)
        RPIQ17B(KEEP=HOSPID AGECAT SEXCAT PAYCAT RACECAT EPIQ17B RPIQ17B LPIQ17B UPIQ17B SPIQ17B XPIQ17B VPIQ17B TPIQ17B PPIQ17B OPIQ17B)
        RPIQ17C(KEEP=HOSPID AGECAT SEXCAT PAYCAT RACECAT EPIQ17C RPIQ17C LPIQ17C UPIQ17C SPIQ17C XPIQ17C VPIQ17C TPIQ17C PPIQ17C OPIQ17C)

        RPIQ18(KEEP=HOSPID AGECAT SEXCAT PAYCAT RACECAT EPIQ18 RPIQ18 LPIQ18 UPIQ18 SPIQ18 XPIQ18 VPIQ18 TPIQ18 PPIQ18 OPIQ18)
        RPIQ19(KEEP=HOSPID AGECAT SEXCAT PAYCAT RACECAT EPIQ19 RPIQ19 LPIQ19 UPIQ19 SPIQ19 XPIQ19 VPIQ19 TPIQ19 PPIQ19 OPIQ19)
        RPIQ20(KEEP=HOSPID AGECAT SEXCAT PAYCAT RACECAT EPIQ20 RPIQ20 LPIQ20 UPIQ20 SPIQ20 XPIQ20 VPIQ20 TPIQ20 PPIQ20 OPIQ20)
        RPIQ30(KEEP=HOSPID AGECAT SEXCAT PAYCAT RACECAT EPIQ30 RPIQ30 LPIQ30 UPIQ30 SPIQ30 XPIQ30 VPIQ30 TPIQ30 PPIQ30 OPIQ30)
        RPIQ31(KEEP=HOSPID AGECAT SEXCAT PAYCAT RACECAT EPIQ31 RPIQ31 LPIQ31 UPIQ31 SPIQ31 XPIQ31 VPIQ31 TPIQ31 PPIQ31 OPIQ31)
        RPIQ32(KEEP=HOSPID AGECAT SEXCAT PAYCAT RACECAT EPIQ32 RPIQ32 LPIQ32 UPIQ32 SPIQ32 XPIQ32 VPIQ32 TPIQ32 PPIQ32 OPIQ32);

 BY HOSPID AGECAT SEXCAT PAYCAT RACECAT;

 LABEL
 PPIQ08  = 'IQI 08 ESOPHAGEAL RESECTION MORTALITY RATE (POPULATION)'
 PPIQ09  = 'IQI 09 PANCREATIC RESECTION MORTALITY RATE (POPULATION)'
 PPIQ09A = 'IQI 09 PANCREATIC RESECTION MORTALITY RATE STRATUM A (POPULATION)'
 PPIQ09B = 'IQI 09 PANCREATIC RESECTION MORTALITY RATE STRATUM B (POPULATION)'
 PPIQ11  = 'IQI 11 ABDOMINAL AORTIC ANEURYSM (AAA) REPAIR MORTALITY RATE (POPULATION)'
 PPIQ11A = 'IQI 11 ABDOMINAL AORTIC ANEURYSM (AAA) REPAIR MORTALITY RATE STRATUM A (POPULATION)'
 PPIQ11B = 'IQI 11 ABDOMINAL AORTIC ANEURYSM (AAA) REPAIR MORTALITY RATE STRATUM B (POPULATION)'
 PPIQ11C = 'IQI 11 ABDOMINAL AORTIC ANEURYSM (AAA) REPAIR MORTALITY RATE STRATUM C (POPULATION)'
 PPIQ11D = 'IQI 11 ABDOMINAL AORTIC ANEURYSM (AAA) REPAIR MORTALITY RATE STRATUM D (POPULATION)'
 PPIQ12  = 'IQI 12 CORONARY ARTERY BYPASS GRAFT (CABG) MORTALITY RATE (POPULATION)'
 PPIQ13  = 'IQI 13 CRANIOTOMY MORTALITY RATE (POPULATION)'
 PPIQ14  = 'IQI 14 HIP REPLACEMENT MORTALITY RATE (POPULATION)'
 PPIQ15  = 'IQI 15 ACUTE MYOCARDIAL INFARCTION (AMI) MORTALITY RATE (POPULATION)'
 PPIQ16  = 'IQI 16 HEART FAILURE MORTALITY RATE (POPULATION)'
 PPIQ17  = 'IQI 17 ACUTE STROKE MORTALITY RATE (POPULATION)'
 PPIQ17A = 'IQI 17 ACUTE STROKE MORTALITY RATE STRATUM A (POPULATION)'
 PPIQ17B = 'IQI 17 ACUTE STROKE MORTALITY RATE STRATUM B (POPULATION)'
 PPIQ17C = 'IQI 17 ACUTE STROKE MORTALITY RATE STRATUM C (POPULATION)'
 PPIQ18  = 'IQI 18 GASTROINTESTINAL HEMORRHAGE MORTALITY RATE (POPULATION)'
 PPIQ19  = 'IQI 19 HIP FRACTURE MORTALITY RATE (POPULATION)'
 PPIQ20  = 'IQI 20 PNEUMONIA MORTALITY RATE (POPULATION)'
 PPIQ30  = 'IQI 30 PERCUTANEOUS CORONARY INTERVENTION (PCI) MORTALITY RATE (POPULATION)'
 PPIQ31  = 'IQI 31 CAROTID ENDARTERECTOMY MORTALITY RATE (POPULATION)'
 PPIQ32  = 'IQI 32 ACUTE MYOCARDIAL INFARCTION (AMI) MORTALITY RATE, WITHOUT TRANSFER CASES (POPULATION)'
 ;

 LABEL
 OPIQ08  = 'IQI 08 ESOPHAGEAL RESECTION MORTALITY RATE (OBSERVED)'
 OPIQ09  = 'IQI 09 PANCREATIC RESECTION MORTALITY RATE (OBSERVED)'
 OPIQ09A = 'IQI 09 PANCREATIC RESECTION MORTALITY RATE STRATUM A (OBSERVED)'
 OPIQ09B = 'IQI 09 PANCREATIC RESECTION MORTALITY RATE STRATUM B (OBSERVED)'
 OPIQ11  = 'IQI 11 ABDOMINAL AORTIC ANEURYSM (AAA) REPAIR MORTALITY RATE (OBSERVED)'
 OPIQ11A = 'IQI 11 ABDOMINAL AORTIC ANEURYSM (AAA) REPAIR MORTALITY RATE STRATUM A (OBSERVED)'
 OPIQ11B = 'IQI 11 ABDOMINAL AORTIC ANEURYSM (AAA) REPAIR MORTALITY RATE STRATUM B (OBSERVED)'
 OPIQ11C = 'IQI 11 ABDOMINAL AORTIC ANEURYSM (AAA) REPAIR MORTALITY RATE STRATUM C (OBSERVED)'
 OPIQ11D = 'IQI 11 ABDOMINAL AORTIC ANEURYSM (AAA) REPAIR MORTALITY RATE STRATUM D (OBSERVED)'
 OPIQ12  = 'IQI 12 CORONARY ARTERY BYPASS GRAFT (CABG) MORTALITY RATE (OBSERVED)'
 OPIQ13  = 'IQI 13 CRANIOTOMY MORTALITY RATE (OBSERVED)'
 OPIQ14  = 'IQI 14 HIP REPLACEMENT MORTALITY RATE (OBSERVED)'
 OPIQ15  = 'IQI 15 ACUTE MYOCARDIAL INFARCTION (AMI) MORTALITY RATE (OBSERVED)'
 OPIQ16  = 'IQI 16 HEART FAILURE MORTALITY RATE (OBSERVED)'
 OPIQ17  = 'IQI 17 ACUTE STROKE MORTALITY RATE (OBSERVED)'
 OPIQ17A = 'IQI 17 ACUTE STROKE MORTALITY RATE STRATUM A (OBSERVED)'
 OPIQ17B = 'IQI 17 ACUTE STROKE MORTALITY RATE STRATUM B (OBSERVED)'
 OPIQ17C = 'IQI 17 ACUTE STROKE MORTALITY RATE STRATUM C (OBSERVED)'
 OPIQ18  = 'IQI 18 GASTROINTESTINAL HEMORRHAGE MORTALITY RATE (OBSERVED)'
 OPIQ19  = 'IQI 19 HIP FRACTURE MORTALITY RATE (OBSERVED)'
 OPIQ20  = 'IQI 20 PNEUMONIA MORTALITY RATE (OBSERVED)'
 OPIQ30  = 'IQI 30 PERCUTANEOUS CORONARY INTERVENTION (PCI) MORTALITY RATE (OBSERVED)'
 OPIQ31  = 'IQI 31 CAROTID ENDARTERECTOMY MORTALITY RATE (OBSERVED)'
 OPIQ32  = 'IQI 32 ACUTE MYOCARDIAL INFARCTION (AMI) MORTALITY RATE, WITHOUT TRANSFER CASES (OBSERVED)'
 ;

 LABEL
 EPIQ08  ='IQI 08 ESOPHAGEAL RESECTION MORTALITY RATE (EXPECTED)'
 EPIQ09  ='IQI 09 PANCREATIC RESECTION MORTALITY RATE (EXPECTED)'
 EPIQ09A ='IQI 09 PANCREATIC RESECTION MORTALITY RATE STRATUM A (EXPECTED)'
 EPIQ09B ='IQI 09 PANCREATIC RESECTION MORTALITY RATE STRATUM B (EXPECTED)'
 EPIQ11  ='IQI 11 ABDOMINAL AORTIC ANEURYSM (AAA) REPAIR MORTALITY RATE (EXPECTED)'
 EPIQ11A ='IQI 11 ABDOMINAL AORTIC ANEURYSM (AAA) REPAIR MORTALITY RATE STRATUM A (EXPECTED)'
 EPIQ11B ='IQI 11 ABDOMINAL AORTIC ANEURYSM (AAA) REPAIR MORTALITY RATE STRATUM B (EXPECTED)'
 EPIQ11C ='IQI 11 ABDOMINAL AORTIC ANEURYSM (AAA) REPAIR MORTALITY RATE STRATUM C (EXPECTED)'
 EPIQ11D ='IQI 11 ABDOMINAL AORTIC ANEURYSM (AAA) REPAIR MORTALITY RATE STRATUM D (EXPECTED)'
 EPIQ12  ='IQI 12 CORONARY ARTERY BYPASS GRAFT (CABG) MORTALITY RATE (EXPECTED)'
 EPIQ13  ='IQI 13 CRANIOTOMY MORTALITY RATE (EXPECTED)'
 EPIQ14  ='IQI 14 HIP REPLACEMENT MORTALITY RATE (EXPECTED)'
 EPIQ15  ='IQI 15 ACUTE MYOCARDIAL INFARCTION (AMI) MORTALITY RATE (EXPECTED)'
 EPIQ16  ='IQI 16 HEART FAILURE MORTALITY RATE (EXPECTED)'
 EPIQ17  ='IQI 17 ACUTE STROKE MORTALITY RATE (EXPECTED)'
 EPIQ17A ='IQI 17 ACUTE STROKE MORTALITY RATE STRATUM A (EXPECTED)'
 EPIQ17B ='IQI 17 ACUTE STROKE MORTALITY RATE STRATUM B (EXPECTED)'
 EPIQ17C ='IQI 17 ACUTE STROKE MORTALITY RATE STRATUM C (EXPECTED)'
 EPIQ18  ='IQI 18 GASTROINTESTINAL HEMORRHAGE MORTALITY RATE (EXPECTED)'
 EPIQ19  ='IQI 19 HIP FRACTURE MORTALITY RATE (EXPECTED)'
 EPIQ20  ='IQI 20 PNEUMONIA MORTALITY RATE (EXPECTED)'
 EPIQ30  ='IQI 30 PERCUTANEOUS CORONARY INTERVENTION (PCI) MORTALITY RATE (EXPECTED)'
 EPIQ31  ='IQI 31 CAROTID ENDARTERECTOMY MORTALITY RATE (EXPECTED)'
 EPIQ32  ='IQI 32 ACUTE MYOCARDIAL INFARCTION (AMI) MORTALITY RATE, WITHOUT TRANSFER CASES (EXPECTED)'
 ;

 LABEL
 RPIQ08  ='IQI 08 ESOPHAGEAL RESECTION MORTALITY RATE (RISK ADJ)'
 RPIQ09  ='IQI 09 PANCREATIC RESECTION MORTALITY RATE (RISK ADJ)'
 RPIQ09A ='IQI 09 PANCREATIC RESECTION MORTALITY RATE STRATUM A (RISK ADJ)'
 RPIQ09B ='IQI 09 PANCREATIC RESECTION MORTALITY RATE STRATUM B (RISK ADJ)'
 RPIQ11  ='IQI 11 ABDOMINAL AORTIC ANEURYSM (AAA) REPAIR MORTALITY RATE (RISK ADJ)'
 RPIQ11A ='IQI 11 ABDOMINAL AORTIC ANEURYSM (AAA) REPAIR MORTALITY RATE STRATUM A (RISK ADJ)'
 RPIQ11B ='IQI 11 ABDOMINAL AORTIC ANEURYSM (AAA) REPAIR MORTALITY RATE STRATUM B (RISK ADJ)'
 RPIQ11C ='IQI 11 ABDOMINAL AORTIC ANEURYSM (AAA) REPAIR MORTALITY RATE STRATUM C (RISK ADJ)'
 RPIQ11D ='IQI 11 ABDOMINAL AORTIC ANEURYSM (AAA) REPAIR MORTALITY RATE STRATUM D (RISK ADJ)'
 RPIQ12  ='IQI 12 CORONARY ARTERY BYPASS GRAFT (CABG) MORTALITY RATE (RISK ADJ)'
 RPIQ13  ='IQI 13 CRANIOTOMY MORTALITY RATE (RISK ADJ)'
 RPIQ14  ='IQI 14 HIP REPLACEMENT MORTALITY RATE (RISK ADJ)'
 RPIQ15  ='IQI 15 ACUTE MYOCARDIAL INFARCTION (AMI) MORTALITY RATE (RISK ADJ)'
 RPIQ16  ='IQI 16 HEART FAILURE MORTALITY RATE (RISK ADJ)'
 RPIQ17  ='IQI 17 ACUTE STROKE MORTALITY RATE (RISK ADJ)'
 RPIQ17A ='IQI 17 ACUTE STROKE MORTALITY RATE STRATUM A (RISK ADJ)'
 RPIQ17B ='IQI 17 ACUTE STROKE MORTALITY RATE STRATUM B (RISK ADJ)'
 RPIQ17C ='IQI 17 ACUTE STROKE MORTALITY RATE STRATUM C (RISK ADJ)'
 RPIQ18  ='IQI 18 GASTROINTESTINAL HEMORRHAGE MORTALITY RATE (RISK ADJ)'
 RPIQ19  ='IQI 19 HIP FRACTURE MORTALITY RATE (RISK ADJ)'
 RPIQ20  ='IQI 20 PNEUMONIA MORTALITY RATE (RISK ADJ)'
 RPIQ30  ='IQI 30 PERCUTANEOUS CORONARY INTERVENTION (PCI) MORTALITY RATE (RISK ADJ)'
 RPIQ31  ='IQI 31 CAROTID ENDARTERECTOMY MORTALITY RATE (RISK ADJ)'
 RPIQ32  ='IQI 32 ACUTE MYOCARDIAL INFARCTION (AMI) MORTALITY RATE, WITHOUT TRANSFER CASES (RISK ADJ)'
 ;

 LABEL
 LPIQ08  ='IQI 08 ESOPHAGEAL RESECTION MORTALITY RATE (LOWER CL)'
 LPIQ09  ='IQI 09 PANCREATIC RESECTION MORTALITY RATE (LOWER CL)'
 LPIQ09A ='IQI 09 PANCREATIC RESECTION MORTALITY RATE STRATUM A (LOWER CL)'
 LPIQ09B ='IQI 09 PANCREATIC RESECTION MORTALITY RATE STRATUM B (LOWER CL)'
 LPIQ11  ='IQI 11 ABDOMINAL AORTIC ANEURYSM (AAA) REPAIR MORTALITY RATE (LOWER CL)'
 LPIQ11A ='IQI 11 ABDOMINAL AORTIC ANEURYSM (AAA) REPAIR MORTALITY RATE STRATUM A (LOWER CL)'
 LPIQ11B ='IQI 11 ABDOMINAL AORTIC ANEURYSM (AAA) REPAIR MORTALITY RATE STRATUM B (LOWER CL)'
 LPIQ11C ='IQI 11 ABDOMINAL AORTIC ANEURYSM (AAA) REPAIR MORTALITY RATE STRATUM C (LOWER CL)'
 LPIQ11D ='IQI 11 ABDOMINAL AORTIC ANEURYSM (AAA) REPAIR MORTALITY RATE STRATUM D (LOWER CL)'
 LPIQ12  ='IQI 12 CORONARY ARTERY BYPASS GRAFT (CABG) MORTALITY RATE (LOWER CL)'
 LPIQ13  ='IQI 13 CRANIOTOMY MORTALITY RATE (LOWER CL)'
 LPIQ14  ='IQI 14 HIP REPLACEMENT MORTALITY RATE (LOWER CL)'
 LPIQ15  ='IQI 15 ACUTE MYOCARDIAL INFARCTION (AMI) MORTALITY RATE (LOWER CL)'
 LPIQ16  ='IQI 16 HEART FAILURE MORTALITY RATE (LOWER CL)'
 LPIQ17  ='IQI 17 ACUTE STROKE MORTALITY RATE (LOWER CL)'
 LPIQ17A ='IQI 17 ACUTE STROKE MORTALITY RATE STRATUM A (LOWER CL)'
 LPIQ17B ='IQI 17 ACUTE STROKE MORTALITY RATE STRATUM B (LOWER CL)'
 LPIQ17C ='IQI 17 ACUTE STROKE MORTALITY RATE STRATUM C (LOWER CL)'
 LPIQ18  ='IQI 18 GASTROINTESTINAL HEMORRHAGE MORTALITY RATE (LOWER CL)'
 LPIQ19  ='IQI 19 HIP FRACTURE MORTALITY RATE (LOWER CL)'
 LPIQ20  ='IQI 20 PNEUMONIA MORTALITY RATE (LOWER CL)'
 LPIQ30  ='IQI 30 PERCUTANEOUS CORONARY INTERVENTION (PCI) MORTALITY RATE (LOWER CL)'
 LPIQ31  ='IQI 31 CAROTID ENDARTERECTOMY MORTALITY RATE (LOWER CL)'
 LPIQ32  ='IQI 32 ACUTE MYOCARDIAL INFARCTION (AMI) MORTALITY RATE, WITHOUT TRANSFER CASES (LOWER CL)'
 ;

 LABEL
 UPIQ08  ='IQI 08 ESOPHAGEAL RESECTION MORTALITY RATE (UPPER CL)'
 UPIQ09  ='IQI 09 PANCREATIC RESECTION MORTALITY RATE (UPPER CL)'
 UPIQ09A ='IQI 09 PANCREATIC RESECTION MORTALITY RATE STRATUM A (UPPER CL)'
 UPIQ09B ='IQI 09 PANCREATIC RESECTION MORTALITY RATE STRATUM A (UPPER CL)'
 UPIQ11  ='IQI 11 ABDOMINAL AORTIC ANEURYSM (AAA) REPAIR MORTALITY RATE (UPPER CL)'
 UPIQ11A ='IQI 11 ABDOMINAL AORTIC ANEURYSM (AAA) REPAIR MORTALITY RATE STRATUM A (UPPER CL)'
 UPIQ11B ='IQI 11 ABDOMINAL AORTIC ANEURYSM (AAA) REPAIR MORTALITY RATE STRATUM B (UPPER CL)'
 UPIQ11C ='IQI 11 ABDOMINAL AORTIC ANEURYSM (AAA) REPAIR MORTALITY RATE STRATUM C (UPPER CL)'
 UPIQ11D ='IQI 11 ABDOMINAL AORTIC ANEURYSM (AAA) REPAIR MORTALITY RATE STRATUM D (UPPER CL)'
 UPIQ12  ='IQI 12 CORONARY ARTERY BYPASS GRAFT (CABG) MORTALITY RATE (UPPER CL)'
 UPIQ13  ='IQI 13 CRANIOTOMY MORTALITY RATE (UPPER CL)'
 UPIQ14  ='IQI 14 HIP REPLACEMENT MORTALITY RATE (UPPER CL)'
 UPIQ15  ='IQI 15 ACUTE MYOCARDIAL INFARCTION (AMI) MORTALITY RATE (UPPER CL)'
 UPIQ16  ='IQI 16 HEART FAILURE MORTALITY RATE (UPPER CL)'
 UPIQ17  ='IQI 17 ACUTE STROKE MORTALITY RATE (UPPER CL)'
 UPIQ17A ='IQI 17 ACUTE STROKE MORTALITY RATE STRATUM A (UPPER CL)'
 UPIQ17B ='IQI 17 ACUTE STROKE MORTALITY RATE STRATUM B (UPPER CL)'
 UPIQ17C ='IQI 17 ACUTE STROKE MORTALITY RATE STRATUM C (UPPER CL)'
 UPIQ18  ='IQI 18 GASTROINTESTINAL HEMORRHAGE MORTALITY RATE (UPPER CL)'
 UPIQ19  ='IQI 19 HIP FRACTURE MORTALITY RATE (UPPER CL)'
 UPIQ20  ='IQI 20 PNEUMONIA MORTALITY RATE (UPPER CL)'
 UPIQ30  ='IQI 30 PERCUTANEOUS CORONARY INTERVENTION (PCI) MORTALITY RATE (UPPER CL)'
 UPIQ31  ='IQI 31 CAROTID ENDARTERECTOMY MORTALITY RATE (UPPER CL)'
 UPIQ32  ='IQI 32 ACUTE MYOCARDIAL INFARCTION (AMI) MORTALITY RATE, WITHOUT TRANSFER CASES (UPPER CL)'
 ;

 LABEL
 SPIQ08  ='IQI 08 ESOPHAGEAL RESECTION MORTALITY RATE (SMOOTHED)'
 SPIQ09  ='IQI 09 PANCREATIC RESECTION MORTALITY RATE (SMOOTHED)'
 SPIQ09A ='IQI 09 PANCREATIC RESECTION MORTALITY RATE STRATUM A (SMOOTHED)'
 SPIQ09B ='IQI 09 PANCREATIC RESECTION MORTALITY RATE STRATUM B (SMOOTHED)'
 SPIQ11  ='IQI 11 ABDOMINAL AORTIC ANEURYSM (AAA) REPAIR MORTALITY RATE (SMOOTHED)'
 SPIQ11A ='IQI 11 ABDOMINAL AORTIC ANEURYSM (AAA) REPAIR MORTALITY RATE STRATUM A (SMOOTHED)'
 SPIQ11B ='IQI 11 ABDOMINAL AORTIC ANEURYSM (AAA) REPAIR MORTALITY RATE STRATUM B (SMOOTHED)'
 SPIQ11C ='IQI 11 ABDOMINAL AORTIC ANEURYSM (AAA) REPAIR MORTALITY RATE STRATUM C (SMOOTHED)'
 SPIQ11D ='IQI 11 ABDOMINAL AORTIC ANEURYSM (AAA) REPAIR MORTALITY RATE STRATUM D (SMOOTHED)'
 SPIQ12  ='IQI 12 CORONARY ARTERY BYPASS GRAFT (CABG) MORTALITY RATE (SMOOTHED)'
 SPIQ13  ='IQI 13 CRANIOTOMY MORTALITY RATE (SMOOTHED)'
 SPIQ14  ='IQI 14 HIP REPLACEMENT MORTALITY RATE (SMOOTHED)'
 SPIQ15  ='IQI 15 ACUTE MYOCARDIAL INFARCTION (AMI) MORTALITY RATE (SMOOTHED)'
 SPIQ16  ='IQI 16 HEART FAILURE MORTALITY RATE (SMOOTHED)'
 SPIQ17  ='IQI 17 ACUTE STROKE MORTALITY RATE (SMOOTHED)'
 SPIQ17A ='IQI 17 ACUTE STROKE MORTALITY RATE STRATUM A (SMOOTHED)'
 SPIQ17B ='IQI 17 ACUTE STROKE MORTALITY RATE STRATUM B (SMOOTHED)'
 SPIQ17C ='IQI 17 ACUTE STROKE MORTALITY RATE STRATUM C (SMOOTHED)'
 SPIQ18  ='IQI 18 GASTROINTESTINAL HEMORRHAGE MORTALITY RATE (SMOOTHED)'
 SPIQ19  ='IQI 19 HIP FRACTURE MORTALITY RATE (SMOOTHED)'
 SPIQ20  ='IQI 20 PNEUMONIA MORTALITY RATE (SMOOTHED)'
 SPIQ30  ='IQI 30 PERCUTANEOUS CORONARY INTERVENTION (PCI) MORTALITY RATE (SMOOTHED)'
 SPIQ31  ='IQI 31 CAROTID ENDARTERECTOMY MORTALITY RATE (SMOOTHED)'
 SPIQ32  ='IQI 32 ACUTE MYOCARDIAL INFARCTION (AMI) MORTALITY RATE, WITHOUT TRANSFER CASES (SMOOTHED)'
 ;

 LABEL
 XPIQ08  ='IQI 08 ESOPHAGEAL RESECTION MORTALITY RATE (SMTHE SE)'
 XPIQ09  ='IQI 09 PANCREATIC RESECTION MORTALITY RATE (SMTHE SE)'
 XPIQ09A ='IQI 09 PANCREATIC RESECTION MORTALITY RATE STRATUM A (SMTHE SE)'
 XPIQ09B ='IQI 09 PANCREATIC RESECTION MORTALITY RATE STRATUM B (SMTHE SE)'
 XPIQ11  ='IQI 11 ABDOMINAL AORTIC ANEURYSM (AAA) REPAIR MORTALITY RATE (SMTHE SE)'
 XPIQ11A ='IQI 11 ABDOMINAL AORTIC ANEURYSM (AAA) REPAIR MORTALITY RATE STRATUM A (SMTHE SE)'
 XPIQ11B ='IQI 11 ABDOMINAL AORTIC ANEURYSM (AAA) REPAIR MORTALITY RATE STRATUM B (SMTHE SE)'
 XPIQ11C ='IQI 11 ABDOMINAL AORTIC ANEURYSM (AAA) REPAIR MORTALITY RATE STRATUM C (SMTHE SE)'
 XPIQ11D ='IQI 11 ABDOMINAL AORTIC ANEURYSM (AAA) REPAIR MORTALITY RATE STRATUM D (SMTHE SE)'
 XPIQ12  ='IQI 12 CORONARY ARTERY BYPASS GRAFT (CABG) MORTALITY RATE (SMTHE SE)'
 XPIQ13  ='IQI 13 CRANIOTOMY MORTALITY RATE (SMTHE SE)'
 XPIQ14  ='IQI 14 HIP REPLACEMENT MORTALITY RATE (SMTHE SE)'
 XPIQ15  ='IQI 15 ACUTE MYOCARDIAL INFARCTION (AMI) MORTALITY RATE (SMTHE SE)'
 XPIQ16  ='IQI 16 HEART FAILURE MORTALITY RATE (SMTHE SE)'
 XPIQ17  ='IQI 17 ACUTE STROKE MORTALITY RATE (SMTHE SE)'
 XPIQ17A ='IQI 17 ACUTE STROKE MORTALITY RATE STRATUM A (SMTHE SE)'
 XPIQ17B ='IQI 17 ACUTE STROKE MORTALITY RATE STRATUM B (SMTHE SE)'
 XPIQ17C ='IQI 17 ACUTE STROKE MORTALITY RATE STRATUM C (SMTHE SE)'
 XPIQ18  ='IQI 18 GASTROINTESTINAL HEMORRHAGE MORTALITY RATE (SMTHE SE)'
 XPIQ19  ='IQI 19 HIP FRACTURE MORTALITY RATE (SMTHE SE)'
 XPIQ20  ='IQI 20 PNEUMONIA MORTALITY RATE (SMTHE SE)'
 XPIQ30  ='IQI 30 PERCUTANEOUS CORONARY INTERVENTION (PCI) MORTALITY RATE (SMTHE SE)'
 XPIQ31  ='IQI 31 CAROTID ENDARTERECTOMY MORTALITY RATE (SMTHE SE)'
 XPIQ32  ='IQI 32 ACUTE MYOCARDIAL INFARCTION (AMI) MORTALITY RATE, WITHOUT TRANSFER CASES (SMTHE SE)'
 ;

 LABEL
 VPIQ08  = 'IQI 08 ESOPHAGEAL RESECTION MORTALITY RATE (VARIANCE)'
 VPIQ09  = 'IQI 09 PANCREATIC RESECTION MORTALITY RATE (VARIANCE)'
 VPIQ09A = 'IQI 09 PANCREATIC RESECTION MORTALITY RATE STRATUM A (VARIANCE)'
 VPIQ09B = 'IQI 09 PANCREATIC RESECTION MORTALITY RATE STRATUM B (VARIANCE)'
 VPIQ11  = 'IQI 11 ABDOMINAL AORTIC ANEURYSM (AAA) REPAIR MORTALITY RATE (VARIANCE)'
 VPIQ11A = 'IQI 11 ABDOMINAL AORTIC ANEURYSM (AAA) REPAIR MORTALITY RATE STRATUM A (VARIANCE)'
 VPIQ11B = 'IQI 11 ABDOMINAL AORTIC ANEURYSM (AAA) REPAIR MORTALITY RATE STRATUM B (VARIANCE)'
 VPIQ11C = 'IQI 11 ABDOMINAL AORTIC ANEURYSM (AAA) REPAIR MORTALITY RATE STRATUM C (VARIANCE)'
 VPIQ11D = 'IQI 11 ABDOMINAL AORTIC ANEURYSM (AAA) REPAIR MORTALITY RATE STRATUM D (VARIANCE)'
 VPIQ12  = 'IQI 12 CORONARY ARTERY BYPASS GRAFT (CABG) MORTALITY RATE (VARIANCE)'
 VPIQ13  = 'IQI 13 CRANIOTOMY MORTALITY RATE (VARIANCE)'
 VPIQ14  = 'IQI 14 HIP REPLACEMENT MORTALITY RATE (VARIANCE)'
 VPIQ15  = 'IQI 15 ACUTE MYOCARDIAL INFARCTION (AMI) MORTALITY RATE (VARIANCE)'
 VPIQ16  = 'IQI 16 HEART FAILURE MORTALITY RATE (VARIANCE)'
 VPIQ17  = 'IQI 17 ACUTE STROKE MORTALITY RATE (VARIANCE)'
 VPIQ17A = 'IQI 17 ACUTE STROKE MORTALITY RATE STRATUM A (VARIANCE)'
 VPIQ17B = 'IQI 17 ACUTE STROKE MORTALITY RATE STRATUM B (VARIANCE)'
 VPIQ17C = 'IQI 17 ACUTE STROKE MORTALITY RATE STRATUM C (VARIANCE)'
 VPIQ18  = 'IQI 18 GASTROINTESTINAL HEMORRHAGE MORTALITY RATE (VARIANCE)'
 VPIQ19  = 'IQI 19 HIP FRACTURE MORTALITY RATE (VARIANCE)'
 VPIQ20  = 'IQI 20 PNEUMONIA MORTALITY RATE (VARIANCE)'
 VPIQ30  = 'IQI 30 PERCUTANEOUS CORONARY INTERVENTION (PCI) MORTALITY RATE (VARIANCE)'
 VPIQ31  = 'IQI 31 CAROTID ENDARTERECTOMY MORTALITY RATE (VARIANCE)'
 VPIQ32  = 'IQI 32 ACUTE MYOCARDIAL INFARCTION (AMI) MORTALITY RATE, WITHOUT TRANSFER CASES (VARIANCE)'
 ;

 RUN;

*==================================================================;
*  TITLE:  PROGRAM P3  PART II:  MERGE PROVIDER RATES FOR AHRQ
*  INPATIENT QUALITY INDICATORS
*
*  DESCRIPTION:  MERGED RATES FOR INPATIENT QUALITY INDICATORS
*
*          >>>  VERSION 5.0 - MARCH, 2015 <<<
*
*===================================================================;

 TITLE2 'PROGRAM P3 PART II';
 TITLE3 'AHRQ INPATIENT QUALITY INDICATORS: PROVIDER-LEVEL MERGED FILES';

 * ---------------------------------------------------------------- ;
 * --- INPATIENT QUALITY INDICATOR MERGED RATES                   - ;
 * ---------------------------------------------------------------- ;

 DATA OUTP3.&OUTFILP3.;
 MERGE INP2.&OUTFILP2.
   (KEEP=HOSPID AGECAT SEXCAT PAYCAT RACECAT _TYPE_ 
         TPIQ01 TPIQ02  TPIQ02A TPIQ02B TPIQ04 TPIQ04A TPIQ04B TPIQ04C TPIQ04D
         TPIQ05-TPIQ07)

       RISKADJ
   (KEEP=HOSPID AGECAT SEXCAT PAYCAT RACECAT 
         EPIQ08 EPIQ09  EPIQ09A EPIQ09B EPIQ11 EPIQ11A EPIQ11B EPIQ11C EPIQ11D EPIQ12-EPIQ16 EPIQ17 EPIQ17A EPIQ17B EPIQ17C EPIQ18-EPIQ20 EPIQ30-EPIQ32
         RPIQ08 RPIQ09  RPIQ09A RPIQ09B RPIQ11 RPIQ11A RPIQ11B RPIQ11C RPIQ11D RPIQ12-RPIQ16 RPIQ17 RPIQ17A RPIQ17B RPIQ17C RPIQ18-RPIQ20 RPIQ30-RPIQ32
		 LPIQ08 LPIQ09  LPIQ09A LPIQ09B LPIQ11 LPIQ11A LPIQ11B LPIQ11C LPIQ11D LPIQ12-LPIQ16 LPIQ17 LPIQ17A LPIQ17B LPIQ17C LPIQ18-LPIQ20 LPIQ30-LPIQ32
		 UPIQ08 UPIQ09  UPIQ09A UPIQ09B UPIQ11 UPIQ11A UPIQ11B UPIQ11C UPIQ11D UPIQ12-UPIQ16 UPIQ17 UPIQ17A UPIQ17B UPIQ17C UPIQ18-UPIQ20 UPIQ30-UPIQ32
		 SPIQ08 SPIQ09  SPIQ09A SPIQ09B SPIQ11 SPIQ11A SPIQ11B SPIQ11C SPIQ11D SPIQ12-SPIQ16 SPIQ17 SPIQ17A SPIQ17B SPIQ17C SPIQ18-SPIQ20 SPIQ30-SPIQ32
		 XPIQ08 XPIQ09  XPIQ09A XPIQ09B XPIQ11 XPIQ11A XPIQ11B XPIQ11C XPIQ11D XPIQ12-XPIQ16 XPIQ17 XPIQ17A XPIQ17B XPIQ17C XPIQ18-XPIQ20 XPIQ30-XPIQ32
		 VPIQ08 VPIQ09  VPIQ09A VPIQ09B VPIQ11 VPIQ11A VPIQ11B VPIQ11C VPIQ11D VPIQ12-VPIQ16 VPIQ17 VPIQ17A VPIQ17B VPIQ17C VPIQ18-VPIQ20 VPIQ30-VPIQ32
		 TPIQ08 TPIQ09  TPIQ09A TPIQ09B TPIQ11 TPIQ11A TPIQ11B TPIQ11C TPIQ11D TPIQ12-TPIQ16 TPIQ17 TPIQ17A TPIQ17B TPIQ17C TPIQ18-TPIQ20 TPIQ30-TPIQ32
		 PPIQ08 PPIQ09  PPIQ09A PPIQ09B PPIQ11 PPIQ11A PPIQ11B PPIQ11C PPIQ11D PPIQ12-PPIQ16 PPIQ17 PPIQ17A PPIQ17B PPIQ17C PPIQ18-PPIQ20 PPIQ30-PPIQ32
		 OPIQ08 OPIQ09  OPIQ09A OPIQ09B OPIQ11 OPIQ11A OPIQ11B OPIQ11C OPIQ11D OPIQ12-OPIQ16 OPIQ17 OPIQ17A OPIQ17B OPIQ17C OPIQ18-OPIQ20 OPIQ30-OPIQ32
         );
 BY HOSPID AGECAT SEXCAT PAYCAT RACECAT;

 %INCLUDE MSXP;

 ARRAY ARRY1{24} EPIQ08-EPIQ09 EPIQ09A EPIQ09B EPIQ11 EPIQ11A EPIQ11B EPIQ11C EPIQ11D EPIQ12-EPIQ17 EPIQ17A EPIQ17B EPIQ17C EPIQ18-EPIQ20 EPIQ30-EPIQ32;
 ARRAY ARRY2{24} RPIQ08-RPIQ09 RPIQ09A RPIQ09B RPIQ11 RPIQ11A RPIQ11B RPIQ11C RPIQ11D RPIQ12-RPIQ17 RPIQ17A RPIQ17B RPIQ17C RPIQ18-RPIQ20 RPIQ30-RPIQ32;
 ARRAY ARRY3{24} LPIQ08-LPIQ09 LPIQ09A LPIQ09B LPIQ11 LPIQ11A LPIQ11B LPIQ11C LPIQ11D LPIQ12-LPIQ17 LPIQ17A LPIQ17B LPIQ17C LPIQ18-LPIQ20 LPIQ30-LPIQ32;
 ARRAY ARRY4{24} UPIQ08-UPIQ09 UPIQ09A UPIQ09B UPIQ11 UPIQ11A UPIQ11B UPIQ11C UPIQ11D UPIQ12-UPIQ17 UPIQ17A UPIQ17B UPIQ17C UPIQ18-UPIQ20 UPIQ30-UPIQ32;
 ARRAY ARRY5{24} SPIQ08-SPIQ09 SPIQ09A SPIQ09B SPIQ11 SPIQ11A SPIQ11B SPIQ11C SPIQ11D SPIQ12-SPIQ17 SPIQ17A SPIQ17B SPIQ17C SPIQ18-SPIQ20 SPIQ30-SPIQ32;
 ARRAY ARRY6{24} XPIQ08-XPIQ09 XPIQ09A XPIQ09B XPIQ11 XPIQ11A XPIQ11B XPIQ11C XPIQ11D XPIQ12-XPIQ17 XPIQ17A XPIQ17B XPIQ17C XPIQ18-XPIQ20 XPIQ30-XPIQ32;
 ARRAY ARRY7{24} VPIQ08-VPIQ09 VPIQ09A VPIQ09B VPIQ11 VPIQ11A VPIQ11B VPIQ11C VPIQ11D VPIQ12-VPIQ17 VPIQ17A VPIQ17B VPIQ17C VPIQ18-VPIQ20 VPIQ30-VPIQ32;
 ARRAY ARRY8{24} PPIQ08-PPIQ09 PPIQ09A PPIQ09B PPIQ11 PPIQ11A PPIQ11B PPIQ11C PPIQ11D PPIQ12-PPIQ17 PPIQ17A PPIQ17B PPIQ17C PPIQ18-PPIQ20 PPIQ30-PPIQ32;

 DO I = 1 TO 24;
   IF ARRY8(I) <= 2 THEN DO;
      ARRY1(I) = .; ARRY2(I) = .; ARRY3(I) = .; ARRY4(I) = .;
      ARRY5(I) = .; ARRY6(I) = .; ARRY7(I) = .; 
   END;
 END;

 DROP I;
    
 FORMAT EPIQ08  EPIQ09  EPIQ09A EPIQ09B EPIQ11 EPIQ11A EPIQ11B EPIQ11C EPIQ11D EPIQ12 EPIQ13 EPIQ14 EPIQ15 EPIQ16 
		EPIQ17  EPIQ17A EPIQ17B EPIQ17C EPIQ18 EPIQ19  EPIQ20 EPIQ30  EPIQ31  EPIQ32

		LPIQ08  LPIQ09  LPIQ09A LPIQ09B LPIQ11 LPIQ11A LPIQ11B LPIQ11C LPIQ11D LPIQ12 LPIQ13 LPIQ14 LPIQ15 LPIQ16
		LPIQ17  LPIQ17A LPIQ17B LPIQ17C LPIQ18 LPIQ19  LPIQ20 LPIQ30 LPIQ31 LPIQ32   

		OPIQ08  OPIQ09  OPIQ09A OPIQ09B OPIQ11 OPIQ11A OPIQ11B OPIQ11C OPIQ11D OPIQ12 OPIQ13 OPIQ14 OPIQ15 OPIQ16
		OPIQ17  OPIQ17A OPIQ17B OPIQ17C OPIQ18 OPIQ19 OPIQ20 OPIQ30 OPIQ31 OPIQ32

		RPIQ08  RPIQ09  RPIQ09A RPIQ09B RPIQ11 RPIQ11A RPIQ11B RPIQ11C RPIQ11D RPIQ12 RPIQ13 RPIQ14 RPIQ15 RPIQ16
		RPIQ17  RPIQ17A RPIQ17B RPIQ17C RPIQ18 RPIQ19  RPIQ20  RPIQ30  RPIQ31  RPIQ32

		SPIQ08  SPIQ09  SPIQ09A SPIQ09B SPIQ11 SPIQ11A SPIQ11B SPIQ11C SPIQ11D SPIQ12 SPIQ13 SPIQ14 SPIQ15 SPIQ16
		SPIQ17  SPIQ17A SPIQ17B SPIQ17C SPIQ18 SPIQ19  SPIQ20 SPIQ30  SPIQ31  SPIQ32

		UPIQ08  UPIQ09  UPIQ09A UPIQ09B UPIQ11 UPIQ11A UPIQ11B UPIQ11C UPIQ11D UPIQ12 UPIQ13 UPIQ14 UPIQ15 UPIQ16
		UPIQ17  UPIQ17A UPIQ17B UPIQ17C UPIQ18 UPIQ19  UPIQ20 UPIQ30 UPIQ31 UPIQ32 

		VPIQ08  VPIQ09  VPIQ09A VPIQ09B VPIQ11 VPIQ11A VPIQ11B VPIQ11C VPIQ11D VPIQ12 VPIQ13 VPIQ14 VPIQ15 VPIQ16 
		VPIQ17  VPIQ17A VPIQ17B VPIQ17C VPIQ18 VPIQ19  VPIQ20 VPIQ30  VPIQ31  VPIQ32 

		XPIQ08  XPIQ09  XPIQ09A XPIQ09B XPIQ11 XPIQ11A XPIQ11B XPIQ11C XPIQ11D XPIQ12 XPIQ13 XPIQ14 XPIQ15 XPIQ16
		XPIQ17  XPIQ17A XPIQ17B XPIQ17C XPIQ18 XPIQ19  XPIQ20  XPIQ30  XPIQ31  XPIQ32 13.7

		TPIQ01  TPIQ02  TPIQ02A TPIQ02B TPIQ04  TPIQ04A TPIQ04B TPIQ04C TPIQ04D TPIQ05 TPIQ06 TPIQ07  TPIQ08  TPIQ09  TPIQ09A TPIQ09B
		TPIQ11  TPIQ11A TPIQ11B TPIQ11C TPIQ11D TPIQ12  TPIQ13  TPIQ14  TPIQ15  TPIQ16 TPIQ17 TPIQ17A TPIQ17B TPIQ17C TPIQ18
		TPIQ19  TPIQ20  TPIQ30 TPIQ31  TPIQ32

		PPIQ08  PPIQ09  PPIQ09A PPIQ09B PPIQ11 PPIQ11A PPIQ11B PPIQ11C PPIQ11D PPIQ12 PPIQ13 PPIQ14 PPIQ15 PPIQ16
		PPIQ17  PPIQ17A PPIQ17B PPIQ17C PPIQ18 PPIQ19  PPIQ20 PPIQ30  PPIQ31  PPIQ32 13.0;

 RUN;

 /***------------------------------------------------------------------------------------------------------------------
    PROVIDER-LEVEL INDICATORS WHICH ARE UTILIZATION OR PROCESS MEASURES: IQI 321, 22, 23, 24, 25, 33 AND 34 ARE NOT 
    RISK ADJUSTED IN THE SOFTWARE BECAUSE RISK-ADJUSTMENT IS NOT APPROPRIATE.   FOR THESE INDICATORS, THE 
    OBSERVED RATE IS THE APPLICABLE MEASURE OF PERFORMANCE, AND THE EXPECTED, RISK-ADJUSTED, AND SMOOTHED RATES PROVIDE 
    NO ADDITIONAL INFORMATION. STARTING IN VERSION 4.5, THE VALUES FOR THOSE RATES ARE OUTPUT FROM IQSASP2.SAS ONLY.
 ---------------------------------------------------------------------------------------------------------------------***/

 * -------------------------------------------------------------- ;
 * --- CONTENTS AND MEANS OF PROVIDER-LEVEL MERGED FILE --------- ;
 * -------------------------------------------------------------- ;

 PROC CONTENTS DATA=OUTP3.&OUTFILP3. POSITION;
 RUN;

 PROC MEANS DATA=OUTP3.&OUTFILP3. (WHERE=(_TYPE_ IN (16))) N NMISS MIN MAX MEAN SUM NOLABELS;
 TITLE4 "SUMMARY OF PROVIDER-LEVEL RATES (_TYPE_=16)";
 RUN;

 * -------------------------------------------------------------- ;
 * ---SAMPLE GRAPH HIGHLIGHTING OBSERVED VS RISK ADJUSTED ------- ;
 * ---Modify CONTROL_IQI to change HOSPID and Measure.    ------- ;
 * -------------------------------------------------------------- ;
 %MACRO PTWHR(CNTRLTST);
	%LET did=%sysfunc(open(OUTP3.&OUTFILP3.));
	%LET HIDTYP =%sysfunc(vartype(&did,1));
	%LET RC=%SYSFUNC(CLOSE(&did.));

   %IF %LENGTH(&CNTRLTST) > 0  AND &HIDTYP = C %THEN %DO;
       	PROC TRANSPOSE data=OUTP3.&OUTFILP3. Out = IQ&&INDNUM.G (rename=(col1=IQ&INDNUM.RSLT _name_ = IQIVar));
	        VAR OPIQ&INDNUM EPIQ&INDNUM LPIQ&INDNUM RPIQ&INDNUM UPIQ&INDNUM;
	        WHERE HOSPID = "&HOSPID.";
	    RUN;
	%END;
	%ELSE %IF %LENGTH(&CNTRLTST) > 0  AND &HIDTYP =N %THEN %DO;
       	PROC TRANSPOSE data=OUTP3.&OUTFILP3. Out = IQ&&INDNUM.G (rename=(col1=IQ&INDNUM.RSLT _name_ = IQIVar));
	        VAR OPIQ&INDNUM EPIQ&INDNUM LPIQ&INDNUM RPIQ&INDNUM UPIQ&INDNUM;
	        WHERE HOSPID = &HOSPID.;
	    RUN;
	%END;
	%ELSE %DO;
	   	PROC TRANSPOSE data=OUTP3.&OUTFILP3. Out = IQ&&INDNUM.G (rename=(col1=IQ&INDNUM.RSLT _name_ = IQIVar));
	        VAR  OPIQ&INDNUM EPIQ&INDNUM LPIQ&INDNUM RPIQ&INDNUM UPIQ&INDNUM;
	        WHERE _TYPE_ = 0;
	    RUN;
    %END;
%MEND PTWHR;
 %PTWHR(&HOSPID);
%LET dsid=%sysfunc(open(OUTP3.&OUTFILP3.));
%LET IQLBL = %sysfunc(varlabel(&dsid,%sysfunc(varnum(&dsid,OPIQ&INDNUM))));
%LET IQTITLE = %TRIM(%SUBSTR(&IQLBL,7,%INDEX(&IQLBL,OBS)-8));
%LET RC=%SYSFUNC(CLOSE(&dsid));

ods graphics on;
Title height = 16pt "&IQTITLE";

data nat;
  Length idchar $8.;
 
  idchar="&INDNUM";

  *-- MAP MEASURE NUM TO ARRAY INDEX SUB_N --* ;
 IF idchar in("8" "08")  THEN SUB_N = 1;

 IF idchar in("9" "09")  THEN SUB_N = 2;
 IF idchar in("9A" "09A") THEN SUB_N = 3;
 IF idchar in("9B" "09B") THEN SUB_N = 4;

 IF idchar = "11"  THEN SUB_N = 5;
 IF idchar = "11A" THEN SUB_N = 6;
 IF idchar = "11B" THEN SUB_N = 7;
 IF idchar = "11C" THEN SUB_N = 8;
 IF idchar = "11D" THEN SUB_N = 9;

 IF idchar = "12"  THEN SUB_N = 10;
 IF idchar = "13"  THEN SUB_N = 11;
 IF idchar = "14"  THEN SUB_N = 12;
 IF idchar = "15"  THEN SUB_N = 13;
 IF idchar = "16"  THEN SUB_N = 14;

 IF idchar = "17"  THEN SUB_N = 15;
 IF idchar = "17A" THEN SUB_N = 16;
 IF idchar = "17B" THEN SUB_N = 17;
 IF idchar = "17C" THEN SUB_N = 18;

 IF idchar = "18"  THEN SUB_N = 19;
 IF idchar = "19"  THEN SUB_N = 20;
 IF idchar = "20"  THEN SUB_N = 21;
 IF idchar = "30"  THEN SUB_N = 27;
 IF idchar = "31"  THEN SUB_N = 28;
 IF idchar = "32"  THEN SUB_N = 29;
 output;

run;

data national;
  set nat;
  Length IQIVarOrd  $20.;

  %INCLUDE MSXP;

  if SUB_N ne . then do;
    result = arryp3{SUB_N}*1000;
    IQIVarOrd = '6 National';
  end;

run;

data IQ&&INDNUM.G;
  set IQ&&INDNUM.G;
  Length IQIVarOrd  $20.;

  if substr(IQIVar,1,3) = 'OPI' then IQIVarOrd = '1 Observed';
  if substr(IQIVar,1,3) = 'EPI' then IQIVarOrd = '2 Expected';
  if substr(IQIVar,1,3) = 'LPI' then IQIVarOrd = '3 Lower CL';
  if substr(IQIVar,1,3) = 'RPI' then IQIVarOrd = '4 Risk Adj';
  if substr(IQIVar,1,3) = 'UPI' then IQIVarOrd = '5 Upper CL';
 
  result = (IQ&INDNUM.RSLT)*1000;

run;


data IQ&&INDNUM.G;
  set IQ&&INDNUM.G national;

  if result = . then delete;

run;

proc sort data=IQ&&INDNUM.G out=IQPLOT;
  by IQIVarOrd;
run;

%Macro hospplot(hosplabel1,hosplabel2);

%if &&indnum ne  %then %do;

%if &&hospid =  %then %do;

proc sgplot data=IQPLOT;
vbar IQIVarOrd /response = result; 
xaxis LABEL = &hosplabel1 discreteorder=data;
YAXIS LABEL = 'Rate per 1000 Discharges';
run;

%end;

%if &&hospid ne  %then %do;

data hospital;
  
proc sgplot data=IQPLOT;
vbar IQIVarOrd /response = result; 
xaxis LABEL = &hosplabel2 discreteorder=data;
YAXIS LABEL = '';
run;

%end;

%end;

%Mend;

%hospplot('Results for All Discharges',%str("Results for Hopsital: &&hospid"));


 * -------------------------------------------------------------- ;
 * --- PRINT PROVIDER MERGED FILE ------------------------------- ;
 * -------------------------------------------------------------- ;

 %MACRO PRT2;

 %IF &PRINT. = 1 %THEN %DO;

 %MACRO PRT(IQ,TEXT,VOLUME);

 PROC  PRINT DATA=OUTP3.&OUTFILP3. LABEL SPLIT='*';
 %IF   &VOLUME = 0 %THEN %DO;
 VAR   HOSPID AGECAT SEXCAT PAYCAT RACECAT 
       TPIQ&IQ. PPIQ&IQ. OPIQ&IQ. EPIQ&IQ. RPIQ&IQ. LPIQ&IQ. UPIQ&IQ. SPIQ&IQ. XPIQ&IQ.;
 LABEL HOSPID  = "HOSPID"
       AGECAT  = "AGECAT"
       SEXCAT  = "SEXCAT"
       PAYCAT  = "PAYCAT"
       RACECAT = "RACECAT"
       TPIQ&IQ.   = "TPIQ&IQ.*(NUMERATOR)"
       PPIQ&IQ.   = "PPIQ&IQ.*(DENOMINATOR)"
       OPIQ&IQ.   = "OPIQ&IQ.*(OBSERVED)"
       EPIQ&IQ.   = "EPIQ&IQ.*(EXPECTED)"
       RPIQ&IQ.   = "RPIQ&IQ.*(RISK ADJ)"
       LPIQ&IQ.   = "LPIQ&IQ.*(LOWER CL)"
       UPIQ&IQ.   = "UPIQ&IQ.*(UPPER CL)"
       SPIQ&IQ.   = "SPIQ&IQ.*(SMOOTHED)"
       XPIQ&IQ.   = "XPIQ&IQ.*(SMTHE SE)"
       ;
 FORMAT AGECAT AGECAT.
        SEXCAT SEXCAT.
        PAYCAT PAYCAT.
        RACECAT RACECAT.
        TPIQ&IQ. PPIQ&IQ. 13.0 OPIQ&IQ. EPIQ&IQ. RPIQ&IQ. LPIQ&IQ. UPIQ&IQ. SPIQ&IQ. XPIQ&IQ. 8.6;
 %END;
 %ELSE %DO;
 VAR   HOSPID AGECAT SEXCAT PAYCAT RACECAT TPIQ&IQ.;
 LABEL HOSPID   = "HOSPID"
       AGECAT  = "AGECAT"
       SEXCAT  = "SEXCAT"
       PAYCAT  = "PAYCAT"
       RACECAT = "RACECAT"
       TPIQ&IQ. = "TPIQ&IQ.*(NUMERATOR)"
       ;
 FORMAT AGECAT AGECAT.
        SEXCAT SEXCAT.
        PAYCAT PAYCAT.
        RACECAT RACECAT.
        TPIQ&IQ. 13.0;
 %END;
 TITLE4 "FINAL OUTPUT";
 TITLE5 "INDICATOR  &IQ.: &TEXT";

 RUN;

 %MEND PRT;

 %PRT(01, ESOPHAGEAL RESECTION VOLUME,1);
 %PRT(02, PANCREATIC RESECTION VOLUME,1);
 %PRT(02A, PANCREATIC RESECTION VOLUME STRATUM A,1);
 %PRT(02B, PANCREATIC RESECTION VOLUME STRATUM B,1);
 %PRT(04, ABDOMINAL AORTIC ANEURYSM (AAA) REPAIR VOLUME,1);
 %PRT(04A, ABDOMINAL AORTIC ANEURYSM (AAA) REPAIR VOLUME STRATUM A,1);
 %PRT(04B, ABDOMINAL AORTIC ANEURYSM (AAA) REPAIR VOLUME STRATUM B,1);
 %PRT(04C, ABDOMINAL AORTIC ANEURYSM (AAA) REPAIR VOLUME STRATUM C,1);
 %PRT(04D, ABDOMINAL AORTIC ANEURYSM (AAA) REPAIR VOLUME STRATUM D,1);
 %PRT(05, CORONARY ARTERY BYPASS GRAFT (CABG) VOLUME,1); 
 %PRT(06, PERCUTANEOUS CORONARY INTERVENTION (PCI) VOLUME,1);
 %PRT(07, CAROTID ENDARTERECTOMY VOLUME,1);
 %PRT(08, ESOPHAGEAL RESECTION MORTALITY RATE,0);
 %PRT(09, PANCREATIC RESECTION MORTALITY RATE,0);
 %PRT(09A, PANCREATIC RESECTION MORTALITY RATE STRATUM A,0);
 %PRT(09B, PANCREATIC RESECTION MORTALITY RATE STRATUM B,0);
 %PRT(11, ABDOMINAL AORTIC ANEURYSM (AAA) REPAIR MORTALITY RATE,0);
 %PRT(11A, ABDOMINAL AORTIC ANEURYSM (AAA) REPAIR MORTALITY RATE STRATUM A,0);
 %PRT(11B, ABDOMINAL AORTIC ANEURYSM (AAA) REPAIR MORTALITY RATE STRATUM B,0);
 %PRT(11C, ABDOMINAL AORTIC ANEURYSM (AAA) REPAIR MORTALITY RATE STRATUM C,0);
 %PRT(11D, ABDOMINAL AORTIC ANEURYSM (AAA) REPAIR MORTALITY RATE STRATUM D,0);
 %PRT(12, CORONARY ARTERY BYPASS GRAFT (CABG) MORTALITY RATE,0);
 %PRT(13, CRANIOTOMY MORTALITY RATE,0);
 %PRT(14, HIP REPLACEMENT MORTALITY RATE,0);
 %PRT(15, ACUTE MYOCARDIAL INFARCTION (AMI) MORTALITY RATE,0);
 %PRT(16, HEART FAILURE MORTALITY RATE,0);
 %PRT(17, ACUTE STROKE MORTALITY RATE,0);
 %PRT(17A, ACUTE STROKE MORTALITY RATE STRATUM A,0);
 %PRT(17B, ACUTE STROKE MORTALITY RATE STRATUM B,0);
 %PRT(17C, ACUTE STROKE MORTALITY RATE STRATUM C,0);
 %PRT(18, GASTROINTESTINAL HEMORRHAGE MORTALITY RATE,0);
 %PRT(19, HIP FRACTURE MORTALITY RATE,0);
 %PRT(20, PNEUMONIA MORTALITY RATE,0);
 %PRT(30, PERCUTANEOUS CORONARY INTERVENTION (PCI) MORTALITY RATE,0);
 %PRT(31, CAROTID ENDARTERECTOMY MORTALITY RATE,0);
 %PRT(32, %BQUOTE(ACUTE MYOCARDIAL INFARCTION (AMI) MORTALITY RATE, WITHOUT TRANSFER CASES),0);


 %END;

 %MEND PRT2;

 %PRT2;

 * -------------------------------------------------------------- ;
 * --- WRITE SAS OUTPUT DATA SET TO COMMA-DELIMITED TEXT FILE --- ;
 * --- FOR EXPORT INTO SPREADSHEETS ----------------------------- ;
 * -------------------------------------------------------------- ;

 %MACRO TEXT;

 %IF &TEXTP3. = 1 %THEN %DO;

 DATA _NULL_;
 SET OUTP3.&OUTFILP3;
 FILE IQTEXTP3 LRECL=4000;
 IF _N_=1 THEN PUT 
 "HOSP ID" ","  "AGE"     "," "SEX"     "," "PAYER"   "," "RACE"  "," "TYPE" ","
 "TPIQ01"  ","  "TPIQ02"  "," "TPIQ02A" "," "TPIQ02B" "," 
 "TPIQ04"  ","  "TPIQ04A" "," "TPIQ04B" "," "TPIQ04C"  "," "TPIQ04D"  ","  
 "TPIQ05"  ","  "TPIQ06"  "," "TPIQ07"  "," 
 "TPIQ08"  ","  "TPIQ09"  "," "TPIQ09A" "," "TPIQ09B" "," 
 "TPIQ11"  ","  "TPIQ11A" "," "TPIQ11B" "," "TPIQ11C" "," "TPIQ11D" ","
 "TPIQ12"  ","  "TPIQ13"  "," "TPIQ14"  "," "TPIQ15"  "," "TPIQ16"  "," 
 "TPIQ17"  ","  "TPIQ17A" "," "TPIQ17B" "," "TPIQ17C" "," "TPIQ18"  "," "TPIQ19"  "," 
 "TPIQ20"  ","  "TPIQ30"  "," "TPIQ31"  "," "TPIQ32"  "," 
 "PPIQ08"  ","  "PPIQ09"  "," "PPIQ09A" "," "PPIQ09B" ","
 "PPIQ11"  ","  "PPIQ11A" "," "PPIQ11B" "," "PPIQ11C" "," "PPIQ11D" ","  
 "PPIQ12"  ","  "PPIQ13"  "," "PPIQ14"  "," "PPIQ15"  "," "PPIQ16"  ","
 "PPIQ17"  ","  "PPIQ17A" "," "PPIQ17B" "," "PPIQ17C" "," "PPIQ18"  ","
 "PPIQ19"  ","  "PPIQ20"  "," "PPIQ30"  "," "PPIQ31"  "," "PPIQ32"  "," 
 "OPIQ08"  ","  "OPIQ09"  "," "OPIQ09A" "," "OPIQ09B" "," 
 "OPIQ11"  ","  "OPIQ11A" "," "OPIQ11B" "," "OPIQ11C" "," "OPIQ11D" "," 
 "OPIQ12"  ","  "OPIQ13"  "," "OPIQ14"  "," "OPIQ15"  "," "OPIQ16"  ","
 "OPIQ17"  ","  "OPIQ17A" "," "OPIQ17B" "," "OPIQ17C" "," "OPIQ18"  "," 
 "OPIQ19"  ","  "OPIQ20"  "," "OPIQ30"  "," "OPIQ31"  "," "OPIQ32"  "," 
 "EPIQ08"  ","  "EPIQ09"  "," "EPIQ09A" "," "EPIQ09B" "," 
 "EPIQ11"  ","  "EPIQ11A" "," "EPIQ11B" "," "EPIQ11C" "," "EPIQ11D" "," 
 "EPIQ12"  ","  "EPIQ13"  "," "EPIQ14"  "," "EPIQ15"  "," "EPIQ16"  ","
 "EPIQ17"  ","  "EPIQ17A" "," "EPIQ17B" "," "EPIQ17C" "," "EPIQ18"  "," 
 "EPIQ19"  ","  "EPIQ20"  "," "EPIQ30"  "," "EPIQ31"  "," "EPIQ32"  "," 
 "RPIQ08"  ","  "RPIQ09"  "," "RPIQ09A" "," "RPIQ09B" "," 
 "RPIQ11"  ","  "RPIQ11A" "," "RPIQ11B" "," "RPIQ11C" "," "RPIQ11D" "," 
 "RPIQ12"  ","  "RPIQ13"  "," "RPIQ14"  "," "RPIQ15"  "," "RPIQ16"  ","
 "RPIQ17"  ","  "RPIQ17A" "," "RPIQ17B" "," "RPIQ17C" "," "RPIQ18"  "," 
 "RPIQ19"  ","  "RPIQ20"  "," "RPIQ30"  "," "RPIQ31"  "," "RPIQ32"  "," 
 "LPIQ08"  ","  "LPIQ09"  "," "LPIQ09A" "," "LPIQ09B" "," 
 "LPIQ11"  ","  "LPIQ11A" "," "LPIQ11B" "," "LPIQ11C" "," "LPIQ11D" ","
 "LPIQ12"  ","  "LPIQ13"  "," "LPIQ14"  "," "LPIQ15"  "," "LPIQ16"  ","
 "LPIQ17"  ","  "LPIQ17A" "," "LPIQ17B" "," "LPIQ17C" "," "LPIQ18"  ","
 "LPIQ19"  ","  "LPIQ20"  "," "LPIQ30"  "," "LPIQ31"  "," "LPIQ32"  "," 
 "UPIQ08"  ","  "UPIQ09"  "," "UPIQ09A" "," "UPIQ09B" ","  
 "UPIQ11"  ","  "UPIQ11A" "," "UPIQ11B" "," "UPIQ11C" "," "UPIQ11D" ","  
 "UPIQ12"  ","  "UPIQ13"  "," "UPIQ14"  "," "UPIQ15"  "," "UPIQ16"  ","
 "UPIQ17"  ","  "UPIQ17A" "," "UPIQ17B" "," "UPIQ17C" "," "UPIQ18"  ","  
 "UPIQ19"  ","  "UPIQ20"  "," "UPIQ30"  "," "UPIQ31"  "," "UPIQ32"  "," 
 "SPIQ08"  ","  "SPIQ09"  "," "SPIQ09A" "," "SPIQ09B" "," 
 "SPIQ11"  ","  "SPIQ11A" "," "SPIQ11B" "," "SPIQ11C" "," "SPIQ11D" "," 
 "SPIQ12"  ","  "SPIQ13"  "," "SPIQ14"  "," "SPIQ15"  "," "SPIQ16"  ","
 "SPIQ17"  ","  "SPIQ17A" "," "SPIQ17B" "," "SPIQ17C" "," "SPIQ18"  "," 
 "SPIQ19"  ","  "SPIQ20"  "," "SPIQ30"  "," "SPIQ31"  "," "SPIQ32"  ","  
 "XPIQ08"  ","  "XPIQ09"  "," "XPIQ09A" "," "XPIQ09B" "," 
 "XPIQ11"  ","  "XPIQ11A" "," "XPIQ11B" "," "XPIQ11C" "," "XPIQ11D" "," 
 "XPIQ12"  ","  "XPIQ13"  "," "XPIQ14"  "," "XPIQ15"  "," "XPIQ16"  ","
 "XPIQ17"  ","  "XPIQ17A" "," "XPIQ17B" "," "XPIQ17C" "," "XPIQ18"  "," 
 "XPIQ19"  ","  "XPIQ20"  "," "XPIQ30"  "," "XPIQ31"  "," "XPIQ32"  "," 
 ;

 PUT HOSPID 13. "," AGECAT 3.  "," SEXCAT 3. "," PAYCAT 3. "," RACECAT 3. "," _TYPE_ 2.  ","
 (TPIQ01-TPIQ02 TPIQ02A TPIQ02B TPIQ04 TPIQ04A TPIQ04B TPIQ04C TPIQ04D
  TPIQ05-TPIQ09 TPIQ09A TPIQ09B TPIQ11 TPIQ11A TPIQ11B TPIQ11C TPIQ11D
  TPIQ12-TPIQ17 TPIQ17A TPIQ17B TPIQ17C TPIQ18-TPIQ20  TPIQ30-TPIQ32) (7.0 ",") ","
 (PPIQ08-PPIQ09 PPIQ09A PPIQ09B PPIQ11  PPIQ11A PPIQ11B PPIQ11C PPIQ11D 
  PPIQ12-PPIQ17 PPIQ17A PPIQ17B PPIQ17C PPIQ18-PPIQ20   PPIQ30-PPIQ32) (13.2 ",") ","
 (OPIQ08-OPIQ09 OPIQ09A OPIQ09B OPIQ11  OPIQ11A OPIQ11B OPIQ11C OPIQ11D 
  OPIQ12-OPIQ17 OPIQ17A OPIQ17B OPIQ17C OPIQ18-OPIQ20   OPIQ30-OPIQ32) (12.10 ",")  ","
 (EPIQ08-EPIQ09 EPIQ09A EPIQ09B EPIQ11  EPIQ11A EPIQ11B EPIQ11C EPIQ11D 
  EPIQ12-EPIQ17 EPIQ17A EPIQ17B EPIQ17C EPIQ18-EPIQ20 EPIQ30-EPIQ32) (12.10 ",")  ","
 (RPIQ08-RPIQ09 RPIQ09A RPIQ09B RPIQ11  RPIQ11A RPIQ11B RPIQ11C RPIQ11D 
  RPIQ12-RPIQ17 RPIQ17A RPIQ17B RPIQ17C RPIQ18-RPIQ20   RPIQ30-RPIQ32) (12.10 ",")  ","
 (LPIQ08-LPIQ09 LPIQ09A LPIQ09B LPIQ11  LPIQ11A LPIQ11B LPIQ11C LPIQ11D
  LPIQ12-LPIQ17 LPIQ17A LPIQ17B LPIQ17C LPIQ18-LPIQ20   LPIQ30-LPIQ32) (12.10 ",")  ","
 (UPIQ08-UPIQ09 UPIQ09A UPIQ09B UPIQ11  UPIQ11A UPIQ11B UPIQ11C UPIQ11D 
  UPIQ12-UPIQ17 UPIQ17A UPIQ17B UPIQ17C UPIQ18-UPIQ20   UPIQ30-UPIQ32) (12.10 ",")  ","
 (SPIQ08-SPIQ09 SPIQ09A SPIQ09B SPIQ11  SPIQ11A SPIQ11B SPIQ11C SPIQ11D 
  SPIQ12-SPIQ17 SPIQ17A SPIQ17B SPIQ17C SPIQ18-SPIQ20   SPIQ30-SPIQ32) (12.10 ",")  ","
 (XPIQ08-XPIQ09 XPIQ09A XPIQ09B XPIQ11  XPIQ11A XPIQ11B XPIQ11C XPIQ11D
  XPIQ12-XPIQ17 XPIQ17A XPIQ17B XPIQ17C XPIQ18-XPIQ20   XPIQ30-XPIQ32) (12.10 ",") 
 ;

 RUN;

 %END;

 %MEND TEXT;

 %TEXT;
